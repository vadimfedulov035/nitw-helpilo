title: Brochures
tags: 
colorID: 0
position: -1343,690
---
Mae: Hey look, some brochures! #line:a08787
===
title: excuse
tags: 
colorID: 0
position: 639,796
---
Mae: Excuse me, but where is everybody? #line:89e929
Janitor:{width=4} It's 10:45. It's closed. #line:7da177
Janitor: Not a lot of folks getting off the last bus to Possum Springs these days. #line:9d6e9b
Janitor: Just you. #line:c1a7ed
<<close>>
<<setAnimInt Janitor drilling 1>>
<<wait 2>>
<<set $janitor to 1>>
[[Isn't there supposed to be someone at the desk?|someone]] #line:b64380
[[So are you the Janitor or something?|something]] #line:cf1de5


===
title: how
tags: 
colorID: 0
position: 315,808
---
Mae: How's that door coming? -_- #line:e37284
Janitor: How's that soda coming? #line:7acd3b
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
===
title: Janitor
tags: 
colorID: 0
position: 383,500
---
<<if $soda is 0>>
	<<if $janitor is 0>>
		[[excuse]]
	<<elseif $janitor is 1>>
		[[squeeze]]
	<<elseif $janitor is 2>>
		[[how]]
	<<endif>>
<<elseif $soda is 1>>
	[[seeya]]
<<endif>>

===
title: Mural
tags: 
colorID: 0
position: -390,738
---
<<if $bus_station_mural is 0>>
	[[wow]]
<<elseif $bus_station_mural is 1>>
	[[visitors]]
<<elseif $bus_station_mural is 2>>
	[[forest]]
<<elseif $bus_station_mural is 3>>
	[[mine]]
<<endif>>
===
title: Phone
tags: 
colorID: 0
position: -127,63
---
<<if $bus_station_phone is 0>>
	[[reception]]
<<elseif $bus_station_phone is 1>>
	[[whosteals]]
<<endif>>

===
title: seeya
tags: 
colorID: 0
position: 1091,733
---
<<setAnimInt Janitor drilling 2>>
<<wait 1>>
Mae: One Freeasscola. #line:58f01d
<<close>>
<<trigger Janitor GrabbyHand>>
<<setHoldItem SodaCan>>
<<setGiveFunction JanitorSodaDrink>>
<<trigger Mae Give>>
<<wait 2>>
<<lookRight Mae>>
<<wait 5>>
<<lookNearest Mae>>
Janitor: Yummers. #line:7c8a0a
Janitor: So what are you doing here? #line:449b6d
<<setAnimInt Janitor drilling 1>>
Mae: I live here. #line:3bbe4b
Mae: Well, lived here. #line:ab4751
Janitor: Huh. Strange. #line:581921
Mae: When do you think that door's gonna be finished? #line:7902db
<<close>>
<<setAnimInt Janitor drilling 2>>
<<wait 1>>
Janitor: Now. #line:88ccdb
<<close>>
<<setAnimInt Janitor drilling 0>>
<<wait .75>>
Janitor: Goodbye. #line:942e26
<<close>>
<<flip Janitor -1>>
<<walk Janitor ExitLeft>>
<<wait 1>>
<<playOneShot event:/bus_station/bus_station_door BusStationDoor>>
<<waitForMove Janitor>>
<<hide Janitor>>
<<setSceneMood LightsOff>>
<<stopDialogue TV>>
<<closeAll>>
<<animate TV Static>>
<<set $janitor to 5>>
Mae: uh. bye. #line:1fe282
===
title: someone
tags: 
colorID: 0
position: 1041,1014
---
Janitor: Closed. #line:d1c54a
Mae: Why are the lights on? Why is the TV on? #line:c1a449
Janitor: I get spooked when I'm here by my lonesome. #line:2b734d
Mae: Oh. #line:52d276

===
title: something
tags: 
colorID: 0
position: 600,1155
---
Janitor: Looks that way. #line:7812bb
Mae: What do you do? #line:0899b7
Janitor: I fix this door. #line:3b29f7
Mae: That's it? #line:ae570f
Janitor: Nope. #line:dfb72e

===
title: squeeze
tags: 
colorID: 0
position: 87,705
---
Mae: Can I squeeze past you? #line:8b1e12
Janitor: Nope. Just broke the damn thing. #line:320604
Mae: When do you think it might be done? #line:d1ed74
Janitor: Right after you go grab me a Fiascola from the machine. #line:981491
Mae: ... #line:ca5497
Mae: Am I paying for this? #line:dd2baf
Janitor: I always rig it when I'm here after hours. #line:26a4e0
Mae: Nice. Free as in free? :) #line:394e29
Janitor: Free as in no one's here to say otherwise. #line:f00c90
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
<<set $janitor to 2>>
<<set $vending_machine_done to 0>>
===
title: TicketStand
tags: 
colorID: 0
position: -1306,427
---
Mae: It's the ticket stand. #line:a6e72c
===
title: TV
tags: 
colorID: 0
position: -626,417
---
<<animate TV Logo>>
<<wait 5.5>>
<<animate TV MalloyTalk>>
TV: Welcome back to Garbo And Malloy! #line:0cba7f
<<animate TV GarboTalk>>
TV: What's in the news today, Malloy? #line:7ebfe0
<<animate TV MalloyTalk>>
TV: Markets were up today! #line:240660
<<animate TV GarboTalk>>
TV: Waaaaaay up! #line:ff7908
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_applause TV>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: I'm looking at a chart and it's pretty impressive! #line:3c8f6c
<<animate TV MalloyTalk>>
TV: The economy added 15,000 jobs #line:b72c6d
TV: mostly in the chart sector #line:50bff0
<<animate TV GarboTalk>>
TV: which is notoriously recession proof! #line:d1750a
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: My my, Garbo, we got a little political there! #line:342f8c
<<animate TV GarboTalk>>
TV: Gotta get political every now and then. #line:903a92
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: I went on one of those internet dates last week. #line:95e899
<<animate TV GarboTalk>>
TV: Oh? #line:001595
<<animate TV GarboTalk>>
TV: Yeah the internet is a truly giving lover. #line:df8e7d
<<animate TV GarboStare>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_cough TV>>
<<wait 3>>
<<animate TV GarboTalk>>
TV: Hey, any lovely ladies out there looking for an eligible bachelor? #line:fceaf6
TV: Get in touch! #line:658baf
<<animate TV GarboLaugh>>
TV: And I'll let you know if I find one! #line:22bd27
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 3>>
<<close>>
<<animate TV Static>>
<<wait 3>>

===
title: VendingMachine
tags: 
colorID: 0
position: 539,251
---
<<if $janitor >= 2 and $soda is 0>>
	Mae: What the? #line:75143a
    Mae: They have Lime Fiasco and he wants a Fiascola? #line:3716b2
    Mae: That's just a waste. #line:058ebb
	<<close>>
	<<wait 1>>
	<<tell VendingMachine to Vend>>
	<<wait 5.5>>
    <<set $can_grab_soda_can to 1>>
    <<set $vending_machine_done to 1>>
    Mae: Okay, just gotta grab it. #line:94d4e3
	//<<set $soda to 1>>
	//Mae: Got it!
<<else>>
	Mae: :) -_- Fiasco Fox you are too dreamy. #line:867df6
    <<eyes Mae Neutral>>
    <<mood Mae Neutral>>
    <<set $vending_machine_done to 1>>
<<endif>>


===
title: wow
tags: 
colorID: 0
position: -765,1046
---
Mae: Wow. When did they put this up? #line:07ec71
Mae: Possum Springs has never looked more... #line:ef66b7
Mae: ...falsely advertised. :\ #line:585471
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
<<set $bus_station_mural to 1>>
===
title: visitors
tags: 
colorID: 0
position: -562,1273
---
Mae: Visitors are going to be so let down when they see the actual town. #line:48c736
Mae: Prepare to be disappointed, ye who enter here! #line:8702a8
Mae: Our Town Motto. #line:e47578
<<set $bus_station_mural to 2>>
===
title: forest
tags: 
colorID: 0
position: -294,1296
---
Mae: State forest up top, #line:7a46d5
Mae: Regular forest everywhere else. #line:5e929b
Mae: Woods, woods, woods. #line:84a270
<<set $bus_station_mural to 3>>

===
title: mine
tags: 
colorID: 0
position: -114,1046
---
Mae: Look at that minecart. #line:1ecaf7
Mae: That's all long gone. #line:88f64b
Mae: Was this made by a time traveler? #line:e1e5ae
<<set $bus_station_mural_done to 1>>
===
title: reception
tags: 
colorID: 0
position: 78,366
---
Mae: It must be such a relief to payphone companies that Possum Springs gets zero cell reception. #line:8773a7
Mae: It'd be cool to call my parents #line:a122a1
Mae: but some jerkhole took the time to actually rip it off. #line:adbeea
Mae: Who steals a phone? #line:c22b41
<<set $bus_station_phone to 1>>
===
title: whosteals
tags: 
colorID: 0
position: -196,433
---
Mae: I wonder what they're doing with that phone. #line:eee3e1
Mae: Sleeping with it at night. #line:9ba608
Mae: Taking it for walks. #line:2752ca
Mae: Holding it tight. #line:3724ad
Mae: Talking into it #line:319501
Mae: to nobody. #line:7a2782
Mae: Smooching it so right. #line:4f7c9b
<<set $bus_station_phone_done to 1>>

===
title: EnterLevel
tags: 
colorID: 0
position: 346,-40
---
<<if $busStation is 0>>
	<<set $busStation to 1>>
<<endif>>
<<playerFace -1>>
<<if $janitor >= 5>>
	<<hide Janitor>>
<<else>>
	<<flip Janitor -1>>
	<<setAnimInt Janitor drilling 1>>
<<endif>>
Mae: Well, this is great. #line:f53579
Mae: I mean I didn't expect a party or anything #line:31f06f
Mae: but I figured *someone* would be here. #line:f25ef7
Mae: ... #line:9b299a
Mae: Welcome home, Mae. #line:64c8e8
===
