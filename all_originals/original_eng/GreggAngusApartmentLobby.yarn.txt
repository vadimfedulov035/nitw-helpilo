title: EnterLevel
tags: 
colorID: 0
position: 1,-107
---
<<if $take_elevator is 1>>
    <<warp Mae MaeAtIntercom>>
    [[TakeElevator]]
<<else>>
    <<if $act is 2 and $day is 2 and $gregg_friendship_quest is 2>>
        [[GreggFQ2Intro]]
    <<endif>>
<<endif>>
===
title: TakeElevator
tags: 
colorID: 0
position: -51,246
---
<<set $take_elevator to 0>>
<<playerFace -1>>
<<wait 1>>
<<play SfxElevatorOpen>>
<<wait 1>>
<<animationTrigger Elevator Open>>
<<wait 2.5>>
<<walk Mae MaeAtElevator>>
<<wait .1>>
<<fadeOut 1>>
<<wait 1>>
<<scene GreggAngusApartment>>
===
title: InitLevel
tags: 
colorID: 0
position: -1,-368
---
<<hide Gregg>>
<<if $act is 2 and $day is 2 and $gregg_friendship_quest is 2>>
    <<show Gregg>>
    <<warp Mae MaeWithGregg>>
    <<playerFace -1>>
    <<cameraLookAt CameraCenter>>
<<endif>>
===
title: GreggFQ2Intro
tags: 
colorID: 0
position: 375,-27
---
<<set $gregg_hat to 1>>
<<playMusic gregg_manic 3>>
Gregg: the bullet went clean through! #line:577abd
Gregg: you can still see the bullet hole in it. #line:b9440c
Mae: how did that not kill him? #line:c53c6a
Gregg: it musta missed all the important parts of the brain. #line:e71048
Mae: wow. #line:6300b7
Gregg: Yeah Great-great granddad was pretty tough. #line:efc7e4
Mae: But isn't that the helmet the fascists were wearing? #line:4f2640
Gregg: Ugh! No! Wrong war! #line:be04c1
Gregg: And wrong country! #line:8cb85e
Gregg: These weren’t the bad guys! #line:8ae181
Gregg: READ SOME HISTORY. #line:51b1aa
Mae: GEEZ! OK! #line:63c67a
Mae: When did you learn all this stuff? #line:2ea73a
Gregg: When people kept saying it was a fascist hat! #line:e9cdb2
Mae: So what are we doing? #line:b8aafc
<<if $did_gregg_friendship_quest_1>>
	Gregg: Continuing our crime spree. #line:09f942
	Mae: Ooooh yeah. #line:7170fe
<<else>>
	Gregg: You up for a crime? #line:176cce
	Mae: Boy, am I ever. #line:e9e653
	Mae: I mean, I kinda did some last night. #line:1a7afa
	Gregg: Oh really? Without me? #line:d0b855
	Mae: Aw dude. #line:b64879
	Mae: You’re always my number one crimefriend. #line:fc105f
	Gregg: That’s beautiful, dude. Likewise. #line:1f8aa2
	Mae: So what’s the plan? #line:f39a32
<<endif>>
Gregg: Follow me! #line:ef61a0
Mae: YES DEAR LEADER #line:69acf2
Gregg: i am both a leader, and a dear. #line:43f54e
<<close>>
<<walk Mae MaeRight>>
<<walk Gregg GreggRight>>
<<wait 1>>
<<irisOut 1 wait>>
<<scene GreggAngusParkingLot>>
===
