title: Bea
tags: 
colorID: 0
position: -8156,1534
---
<<if $beer_phase is 0>>
	<<if $bea is 0>>
		Bea: What? #line:3a9965
		Mae: Nothing. #line:a402a5
		<<set $bea += 1>>
	<<elseif $bea is 1>>
		Mae: Cool party! #line:a32de1
		Bea: Yep. #line:346938
		<<set $bea += 1>>
	<<elseif $bea is 2>>
		Mae: Whoya talking to? #line:56b0a4
		Bea: Not you. #line:00e569
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 1>>
	<<if $bea is 0>>
        Mae: =_= #line:144859
		Mae: Yooooo. #line:f5e99f
		Bea: What? #line:776ec6
		Mae: You’re kinda grouchy, did you know that? #line:f57914
		Bea: No. I didn’t. #line:11b87e
		Jackie: Rude. #line:147744
        Mae: o_o :| _o_ #line:23d6be
		<<set $bea += 1>>
	<<elseif $bea is 1>>
        Mae: =_= #line:7b1bdf
		Mae: How long are you planning on staying? #line:91c72b
		Bea: Mae, we just got here like 10 minutes ago. #line:42f444
		Mae: Oh. #line:b94b09
		Bea: Are you... how many beers have you had? #line:7d7256
		Mae: TWO! #line:fc4658
		Bea: Pace it out, Mae. You’re tiny. #line:f74988
        Mae: o_o :| _o_ #line:323674
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 2>>
	<<if $bea is 0>>
        Mae: =_= :) #line:39e9fb
		Mae: HEY BEA #line:33a46c
		Bea: Yeah? #line:de76da
		Bea: Oh god. #line:29b05a
		Bea: You’re drunk. #line:bd61a1
		Bea: Already. #line:656ff2
		Mae: That’s why we came out here right??? #line:bd2ebf
		Bea: Not really. I have to drive. #line:97dbd0
		Bea: And even if I didn’t... #line:00d815
		Mae: Well I don’t have my license!!! \o/ #line:182f92
		Mae: So wooohooo for meeeee!!!! #line:4fb043
        Mae: o_o :| _o_ #line:17daa5
		<<set $bea += 1>>
	<<elseif $bea is 1>>
		Mae: HEY BEA #line:4c016c
		Bea: What? #line:daf1b1
		Jackie: Who is that? #line:6c779e
		Bea: Remember Mae Borowski? #line:a10767
		Jackie: Oh god. #line:2b7435
		<<set $bea += 1>>
	<<elseif $bea is 2>>		
		Bea: Mae, you’re drunk. #line:c58a41
		Bea: You need to go chill by the fire. #line:f8fbc9
        Mae: :) #line:cb8d92
		Mae: I’m gonna go run around naked in the wooooods!!!!! \o/ #line:b6caea
		Bea: GO. SIT. NOW. #line:bcfc63
        Mae: o_o :| _o_ #line:938ed9
        <<set $should_sit_log to 1>>
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 3>>
<<endif>>


===
title: EnterLevel
tags: 
colorID: 0
position: -8465,1136
---
<<set $night to 1>>
[[SceneStart]]
===
title: Beer
tags: 
colorID: 0
position: -8811,1242
---
<<if $can_drink_beer is 1>>
	<<set $beer_phase += 1>>
	<<fadeOut 1 wait>>
	<<wait 1>>
    <<playAmbientSFX AmbientSFX:beer_drink>>
    <<wait 3>>
	<<fadeIn 1 wait>>
	[[SceneStart]]
<<else>>
	Mae: I don't know if I should have another drink right now. #line:097e53
<<endif>>

===
title: SceneStart
tags: 
colorID: 0
position: -8573,1604
---
<<set $bea to 0>>
<<set $gregg_angus to 0>>
<<set $cole to 0>>

<<set $angus_done to 0>>
<<set $bea_done to 0>>
<<set $gregg_done to 0>>
<<set $cole_done to 0>>

<<set $can_drink_beer to 0>>

<<if $beer_phase is 0>>
    Mae: =_= :\ #line:6233f0
	Mae: Ugh. #line:6ce028
	Mae: That was awful. #line:ec4077
	Mae: I hate beer. #line:77b8fa
	Mae: I can't remember the last time I drank beer. #line:6788a4
	Mae: It’s not a delicious drink at all. #line:2f5ea0
    Mae: o_o :| _o_ #line:bcee4c
<<elseif $beer_phase is 1>>
    Mae: =_= #line:53e5b9
	Mae: *sigh* #line:e841f6
	Mae: I guess beer is just one of those horrible things adults do. #line:c20c59
	Mae: Like doing taxes and having babies. #line:00af69
    Mae: o_o :| _o_ #line:705e38
<<elseif $beer_phase is 2>>
    Mae: O_O #line:c4b706
	Mae: Screw it! I’m doing two beers! #line:0ab8de
    Mae: :) #line:994f03
	Mae: I’m doing beer! #line:9b7577
	<<if $ate_tacos>>
		Mae: I still taste tacos! #line:571999
	<<endif>>
	Mae: OK?! #line:5e0dce
	Bea: What? #line:7696b0
	Mae: NOTHING! #line:dd4982
    Mae: o_o :| _o_ #line:c2083a
<<endif>>

===
title: End
tags: 
colorID: 0
position: -9034,1731
---
<<fadeOut .5 wait>>
<<scene BeaCar>>
===
title: GreggAngus_BP0
tags: 
colorID: 0
position: -7769,1470
---
<<if $gregg_angus is 0>>
	Mae: Hey dudes. #line:fc9c9f
	Gregg: Yo did you see Cole’s here? #line:d7d552
    <<lookOut Mae>>
    <<dilate Mae .75 .5>>
	Mae: OH GOD. #line:a02a2e
    <<wait 1>>
	Angus: You ok? #line:254f8d
    <<lookNearest Mae>>
    <<dilate Mae 1 .5>>
	Mae: Yeah. Totally fine. #line:a1ecdd
	Gregg: You gonna talk to him? #line:8dd6d3
	[[NO.|GreggAngus_No]] #line:b7e814
	[[Yeah sure.|GreggAngus_YeahSure]] #line:034a8e
<<elseif $gregg_angus is 1>>
    Mae: =_= #line:2bbbf6
	Mae: Urg. #line:7b6b89
    Mae: \o/ #line:7ced98
	Mae: He's just standing there! Like it’s no big deal! #line:a01592
	Gregg: That jerk. #line:d4f887
	Angus: Totally. #line:c77c6a
    Mae: _o_ o_o :| #line:508179
	Mae: You know what? I’m not gonna stand by #line:792f7b
	Mae: and let him stand by #line:4575c5
	Mae: while my night gets ruined! #line:30532b
	Gregg: Yeah! #line:d17974
	Mae: I’m going over there. I'm gonna talk to Cole. #line:5b4146
	Angus: Ruin your own evening! #line:fbca8a
	Gregg: If anyone's gonna ruin your night, Mae, it really should be you. #line:2f98ae
    Mae: :) #line:89f513
	Mae: Thanks guys. #line:da4247
    Mae: :| #line:fc1965
	<<set $talking_to_cole to 1>>
    <<set $cole_done to 0>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 2>>
	Mae: Can you guys believe this dude playing guitar? #line:dc5060
	Gregg: Yep. Playing the douchebag hits. #line:20bb57
	Angus: I like this song! #line:2c3b3d
	Angus: Is this a douchebag song? #line:5d8652
	<<set $douchebag_song to 1>>
	Mae: Naw don’t worry big guy. #line:7473f8
	Angus: ... #line:58bd1c
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
	Gregg: There is a douchebag like this at every party. #line:4665b3
	Mae: You have no idea, Gregg. #line:b64557
	Mae: Back in school you’d always have some frat boy #line:477618
	Mae: hanging out on the lawn #line:659750
	Mae: trying to look all sensitive. #line:950ad6
	<<set $try_to_look_sensitive to 1>>
	Angus: Why is this a douchebag song??? #line:4c1648
	<<set $angus_done to 1>>
	<<set $gregg_done to 1>>
<<endif>>

===
title: GreggAngus_YeahSure
tags: 
colorID: 0
position: -7618,1797
---
<<set $talking_to_cole to 1>>
<<set $gregg_angus += 1>>
<<set $cole_done to 0>>
Gregg: Yeah man! #line:52696d
Angus: You sure, Mae? #line:c59bc6
Mae: Yeah! I mean why should I be weird about it? #line:a187cc
Gregg: Well, I mean, you did write that song afterwards... #line:050637
[[GreggAngus_Song]]

===
title: GreggAngus_No
tags: 
colorID: 0
position: -7885,1806
---
Angus: Probably a good idea. #line:509b2a
Gregg: I mean, you did write that song... #line:bc8600
[[GreggAngus_Song]]
===
title: GreggAngus_Song
tags: 
colorID: 0
position: -7778,2157
---
Gregg: What was it called? #line:46a0cf
Angus: "Go Get Dead, Angel Face"? #line:83c1d1
Mae: Uuuuuuuuuuuugh. #line:d048a5
Mae: We don’t talk about Go Get Dead, Angel Face. #line:efbe7f
<<set $gregg_angus += 1>>
===
title: Cole
tags: 
colorID: 0
position: -6862,1423
---
<<if $beer_phase is 0>>
	Mae: Oh god. It’s... #line:433c74
	<<if $talking_to_cole is 1>>
		Mae: Maybe I need another drink. #line:48cd8a
		<<set $can_drink_beer to 1>>
	<<else>>
        <<set $cole_done to 1>>
    <<endif>>
<<elseif $beer_phase is 1>>
	<<if $cole is 0>>
		<<if $whats_your_name is 1>> 
			Mae: Oh hey, I’m Mae! What’s your name? #line:2245fb
			Cole: Is that you, Mae? #line:11a3d4
			Mae: Oh, I think I know you from somewhere... #line:2dbaa2
			Cole: Uh yeah. Highschool. #line:a51d3b
			Mae: Probably when we were lovers #line:82083b
			Cole: Ha ha. Yeah I guess. #line:c63db0
			Mae: and we beat up that street gang #line:cd03f1
			Cole: Oh yeah! #line:4de03e
			Mae: with chains and garbage cans. #line:c5e7e6
			Cole: Good times, Mae. #line:293628
			Cole: I still have that game, by the way. #line:c59c7d
			Cole: My girlfriend back in school - #line:6d1b7c
            Mae: \o/ :( #line:8bacd6
			Mae: What the hell is your problem? #line:a5c43c
			Cole: What? #line:b747fd
            Mae: o_o :| _o_ #line:22ae6d
		<<else>>
			Mae: Heyyyyy. #line:3687e4
			Cole: Mae? Is that you? #line:e9d03b
			Cole: Wow. How are you? #line:8d826e
			<<if $dropout_week is 1>>
				Mae: Well earlier this week I dropped out of school #line:61e519
				Mae: and now I’m drinking in the woods. #line:bb76a3
				Cole: Wow ok! #line:2d1933
				Mae: What the hell is your problem? #line:f30c57
				Cole: What? #line:6ab765
			<<elseif $hows_it_going is 1>>
				Mae: Hi, I’m Mae! How’s it going? #line:2ff1a8
				Cole: I just asked you that? #line:563b0e
				Cole: I mean, I’m fine. #line:ab2f5f
				Cole: Just back visiting for the weekend. #line:7312ad
				Mae: Really? That’s fascinating! #line:435443
				Cole: I guess. #line:568076
				Cole: I think that’s the first time in a while #line:2bd0bf
				Cole: anyone has called my life fascinating. #line:ec91e0
				Mae: That is so true! Well, see you later!  #line:dd7ee3
				Cole: Ok... #line:e36db3
				Mae: What the hell is your problem? #line:3b4056
				Cole: What? #line:62d023
			<<endif>>
		<<endif>>
        <<set $cole += 1>>
		<<set $has_talked_to_cole_1 to 1>>
	<<elseif $cole is 1>>
		Mae: Be cool, Mae. #line:2b0279
		Mae: Go get a drink and be cool. #line:a14877
		<<set $can_drink_beer to 1>>
	<<endif>>
<<elseif $beer_phase is 2>>
    Mae: Ugh, I don't know if I can do this. #line:343d1c
<<endif>>
===
title: GreggAngus
tags: 
colorID: 0
position: -7517,1023
---
<<if $beer_phase is 0>>
	[[GreggAngus_BP0]]
<<elseif $beer_phase is 1>>
	[[GreggAngus_BP1]]
<<elseif $beer_phase is 2>>
	[[GreggAngus_BP2]]
<<endif>>
===
title: GreggAngus_BP1
tags: 
colorID: 0
position: -7417,1439
---
<<if $gregg_angus is 0>>
	<<if $has_talked_to_cole_1 is 1>>
		Mae: AAAAAAAGH #line:813868
		Gregg: How’d it go? #line:98935e
		Mae: AAAAAAAAGH #line:472a97
		<<set $can_drink_beer to 1>>
	<<endif>>

	<<if $people_dont_like is 1>>
        Mae: =_= :\ #line:862e61
		Mae: Ugh #line:293345
		Angus: You ok? #line:ffff48
		Mae: Sometimes I think that people just don’t like me. #line:346c50
		Gregg: Aw dude. #line:795961
		Gregg: That sucks. #line:aad6ee
		Angus: Yeah Mae you shouldn’t be so down on yourself. #line:053e02
		Gregg: Yeah, who cares if people don’t like you? #line:23f261
		Angus: Ok. That’s not what I- #line:4d39ec
		Gregg: SCREW EVERYONE MAE’S THE BEST!!! D: \o/ #line:e6786a
		<<set $maes_the_best to 1>>
		<<lookAt Gregg DoucheBag>>
		<<lookAt Mae DoucheBag>>
		DoucheBag: Yeah buddy! #line:623c11
		Gregg: SHUT UP DOUCHEBAG! _o_ :| #line:e1575a
		DoucheBag: ...what? #line:8d56e7
		<<lookAt Gregg Mae>>
		<<lookAt Mae Gregg>>
        Mae: :) #line:f52cc8
		Mae: Thanks guys. #line:1e977d
        Mae: o_o :| #line:c3033d
		<<lookNearest Gregg>>
		<<lookNearest Mae>>
	<<elseif $you_dont_like is 1>>
        Mae: =_= #line:752b93
		Mae: Ugh. #line:2bcd2d
		Angus: Problem? #line:1b0e50
		Mae: Sometimes I think I just don’t like people. #line:59f143
		Gregg: Yeah man eff the world. #line:0a8bfa
		Angus: Yeah eff it. #line:6f6770
		Mae: God someone really should just eff the world already. #line:fd6568
		Gregg: The world has gone uneffed for too long. #line:d90a9d
		Angus: Effin’ right. #line:8f9c99
        Mae: o_o :| #line:a83f23
		<<set $eff_the_world to 1>>
	<<endif>>

	<<set $gregg_angus += 1>>
	
<<elseif $gregg_angus is 1>>
    Mae: O_o #line:6a83e5
	Mae: Did you guys hear something? #line:5477d9
	Angus: Like what? #line:f7abf4
	Mae: Something in the woods over there... #line:953f6d
	Gregg: Oh yeah two people are making out on the rock behind us. #line:b5931f
	Mae: That can’t be very comfortable. #line:0c4a05
	Gregg: Who needs comfort when you have makeouts? #line:c951bb
	Angus: But preferably you have both. #line:744c0b
	Gregg: It ain’t a perfect world, Angus. #line:a6ead1
    Mae: o_o #line:c9d912
	<<set $rock_makeouts to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 2>>
	Mae: They really are going at it up there. #line:6ab81c
	Gregg: They suuuuuuure are. #line:fb8558
	Mae: You guys wanna... I dunno... stand somewhere else? #line:dce1ad
	Gregg: Naw dude! #line:005871
	Angus: Why should we move? #line:6dd4aa
	Gregg: They’re the indecent ones. #line:1f61e2
	Angus: We are gentlemen. #line:094689
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
    Mae: =_= #line:455855
	Mae: I’m starting to feel these two beers. #line:8a55cf
	Angus: Yeah. I don’t really drink much. #line:fc2788
	Mae: I only had one beer before. #line:5616cb
	Gregg: Ha ha ha yeah and you spit it out! #line:46138d
	Angus: Mae if you don’t drink you really should be careful. #line:4438a6
	Mae: Naw man, I’m cool. #line:67ff3b
	Angus: Ok, well we’ll keep an eye on you. #line:2c1ce4
	Gregg: We will watch you like hawks. #line:086fee
	Angus: Chaperone hawks. #line:662a17
    Mae: :) #line:3ea21a
	Mae: Thanks dudes. #line:3c9e9c
    Mae: o_o :| #line:16c6f1
	<<set $chaperone_hawks to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 4>>
	Gregg: Yep, they’re still making out. #line:2bcddd
	<<set $gregg_done to 1>>
	<<set $angus_done to 1>>
<<endif>>

===
title: GreggAngus_BP2
tags: 
colorID: 0
position: -7184,1435
---
<<if $gregg_angus is 0>>
	<<if $porn_log is 1 and $rock_makeouts is 1>>
        Mae: =_= :) #line:26f4ab
		Mae: HEY GREGGGG! #line:a4cfb6
		Gregg: Whoa dude. #line:d11a3d
		Angus: You ok, Mae? #line:405308
		Mae: do you think the people makin’ out on #line:b0c628
		Mae: on that rock #line:2c466d
		Mae: met up there because #line:b7fa7f
		Mae: THERE’S A PORN LOGGG??? #line:30e54b
		Gregg: OH MY GOD I BET THERE IS. #line:55f20b
		Angus: A what? #line:18059f
		<<lookAt Gregg Angus>>
		Gregg: You see it’s a log #line:190adb
		<<lookAt Gregg Mae>>
		Mae: where oldendays people #line:4e41f7
		Mae: WOULD PUT POOOORN #line:54be6a
		<<lookAt Gregg Angus>>
		Angus: How old we talking? #line:584b21
		Angus: Like Revolutionary War era? #line:79a63b
		Angus: I think there’s an old cemetery from then back here somewhere... #line:4eeb7f
		<<lookAt Gregg Mae>>
		Mae: These woods are full of old ghosts and porn logs and people making out!!! #line:1ab5df
	    Mae: o_o :| #line:ec1c99
		<<lookNearest Gregg>>
    <<else>>
        Mae: =_= :) #line:eb2a18
		Mae: HEY GREGGGG! #line:6620e6
		Gregg: Whoa dude. Are you ok? #line:b80d12
		Mae: Hey Gregg did you know I think #line:4eb4ce
		Mae: you an Angyyy #line:937511
		Mae: ... #line:522857
		Angus: “Angy?” #line:247058
		Mae: ARE SO CUUUUTE!!! #line:103f33
		Gregg: I did know that, dude. #line:8facb8
		Mae: \o/ #line:692289
        Mae: well GOOD #line:ad2330
        Mae: o_o :| _o_ #line:a61fed
	<<endif>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 1>>
	<<if $dad_knows_angus is 1>>
        Mae: =_= :) #line:8aafa0
		Mae: Hey Angyyyy... my dad knows your dad! #line:37ad6f
		Mae: They used to worrrk together back in the 80s. #line:9023e0
		Angus: Ok. #line:9e0b35
		Mae: He used to hang out at the barrr with your daddddy. #line:43c371
		Gregg: Dude. #line:56b5a1
		Gregg: Don’t ever say “daddy” again! #line:0bc76f
		<<if $stop_saying_daddy is 1>>
			Gregg: You’re saying “daddy”! #line:c00569
			Gregg: Douchebag’s playing a song about someone’s "daddy"! #line:bd0e82
			Gregg: I can’t deal with this! #line:f31b75
            Gregg: My skin is crawling off of my skeleton! #line:514c0a
		<<endif>>
		Mae: pssst! #line:14730a
		Angus: What? #line:3463c3
		Mae: My dad had to stop drinkin’ because #line:e421e9
		Mae: he was a danger to me n’ my mom. #line:3d0bef
		Mae: HA HA HA HA HA HA HA HA HA!!! #line:f7d196
		Gregg: Dude. #line:ef7a3b
		Angus: Ok Mae you’re not allowed to drink anymore. #line:0fb41a
		Angus: Ever. #line:b276a8
        Mae: o_o :| _o_ #line:b9846f
		<<set $gregg_angus += 1>>
	<<else>>
		<<set $gregg_angus += 1>>
		[[GreggAngus_BP2]]
	<<endif>>
<<elseif $gregg_angus is 2>>
	Angus: Ok Mae I’m a little worried about you. #line:4b7438
	<<if $chaperone_hawks is 1>>
		Gregg: We failed at being chaperone hawks. #line:528581
		Angus: Someday, Bug. #line:517484
        Mae: :) #line:b0a60a
		Mae: Awwwww. I forgot you called him Buuuuug. #line:73c0ca
		<<set $knows_gregg_is_bug to 1>>
		Gregg: ... #line:d83fdc
        Angus: ... #line:115aa5
	<<endif>>
    Mae: =_= #line:7ef1e6
	Mae: Well I’m having a great time here in the woods ok. #line:1be2b0
	Angus: Gregg, don’t you think Mae should maybe sit down? #line:a086f5
	Gregg: Yeah dude. You should sit. #line:9c99b4
    Mae: o_o :| _o_ #line:4ed7f6
    <<set $should_sit_log to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
	Gregg: Dude sit down by the fire and chill, ok? #line:83c21b
    Mae: o_o :| _o_ #line:afd21d
    <<set $should_sit_log to 1>>
	<<set $gregg_done to 1>>
	<<set $angus_done to 1>>
<<endif>>

===
title: Gregg
tags: 
colorID: 0
position: -7854,873
---
[[GreggAngus]]
===
title: ColeByFireEnding
tags: 
colorID: 0
position: -6553,1431
---
<<cameraLookAt CameraEnd 3>>
<<wait 6>>
<<flip Cole -1>>
<<wait 1>>
<<walk Cole ColeByFire wait>>
Cole: Hey Mae. #line:8d55fe
Mae: =_= #line:e15d6c
Mae: Hey Cooole-io. #line:be1e17
Cole: Wow. Haven't heard that in forever.  #line:b6b335
Mae: Iz been like THREE YEARS! #line:1487b0
Mae: Thaz not long AT ALL! #line:22f099
Cole: ...well, it was good to see you. #line:432410
Cole: I’m just back for a couple days from school, and- #line:5ab076
Mae: SHUT UP ALREADY. #line:2f0e17
Cole: What? #line:f80ea3
Mae: o_o #line:14d4dc
<<if $no_smooches is 1>>
	Mae: We never even kissed! #line:a39a08
	Cole: Didn't we? #line:1a510a
	Mae: NO! NOT SUCCESSFULLY!!  #line:af5327
	Cole: I'm... sorry? #line:151c68
	Mae: I DON’T EVEN CARE! #line:2c9ec2
<<endif>>
<<if $whatever_god is 1>>
	Mae: I was like SO worried you were gonna be here. #line:20c4f7
	Cole: Oh. #line:7ff89e
	Mae: I PRAYED TO WHATEVERGOD!!! #line:b6abe0
	Cole: what? #line:f39ec1
	Mae: I FIGURED SOMEONE WOULD LISTEN! #line:5aabb4
	Mae: BUT NO! #line:382a1f
	Mae: BECAUSE HERE YOU ARE! #line:e638ea
<<endif>>
<<if $not_cute_ones is 1>>
	Mae: The girl at the video store is all like #line:df7dd5
	Mae: Woowoo! CUTE BOYS!!! #line:2a4a6a
	Mae: and you're a cute boy, cole #line:6b8394
    Cole: Oh! Thanks Mae, I- #line:455afc
	Mae: BUT I HATE CUTE PEOPLE!!! #line:8cd589
	Mae: CUTE IS THE WORST!!! #line:a422b8
<<endif>>
Cole: Ok, well I’m gonna go... #line:1c878c
<<close>>
<<playerJump>>
<<lookAt Mae Cole>>
<<wait 1>>
<<lookOut Mae>>
Mae: o_o #line:5681c0
<<dilate Mae .75 .25>>
Mae: And iz not my fault I'm a total trashfire! #line:027414
<<flip Bea -1>>
<<clearBGChatPartner Bea>>
<<walk Bea BeaByFire>>
<<walk Gregg GreggByFire>>
<<walk Angus AngusByFire>>
<<set $nightmare_eyes to 1>>
<<fadeOutAmbientSFX AmbientSFX:DoucheBagGuitar 1>>
<<fadeOutAmbientSFX AmbientSFX:Walla 1>>
<<wait 1>>
<<setAnimInt DoucheBag state 0>>
<<if $nightmare_eyes>>
    Mae: I GOT NIGHTMARE EYES!!!!! O_O #line:90b0ef
    Mae: And the lazt thing you see iz my eyes oudside the window o_o #line:8491a5
    Mae: And then I ztab you with my knife!!! :) \o/ #line:56cbea
    Mae: STAB STAB STAAAAABBB!!!! :| /o\ #line:5bae82
    Mae: Boom! Dead! _o_ #line:39b37e
    Mae: When the police get there you’re all #line:38c706
    Mae: {width=4}[color=aaa][shake=.01][wave]N I G H T M A R E  E Y E S.[/all] #line:fb2c69
    Gregg: NEW NICKNAME! #line:8f860e
<<elseif $dog_bite_dead>>
    Mae: YOU SHOULD HAVE SEEN THE DOG THAT BIT MY EAR. \o/ #line:76605e
    Cole: What? #line:f0c3c2
    Mae: Yeah he grabbed on and wouldn't let go! #line:23776a
    Mae: And he was like RAWARWARRAWARARARR #line:5965e0
    Mae: And I thought I was gonna DIE!!! #line:9bd12e
    Mae: But then I grabbed a STICK! #line:d026c1
    Mae: And I beat his ass with it! #line:bdeed1
    Mae: And I'll do the same thing to anyone!!!! #line:62077d
    Mae: _o_ #line:69d57d
<<endif>>
Mae: SO DON'T EFF WITH ME, KID! #line:ef43ee
<<if $eff_the_world>>
	Gregg: EFF THE WORLD! #line:7fc1fe
<<endif>>
Bea: Ok Mae, you're threatening people now. #line:1ca115
<<if $body_posi>>
	Mae: No Bea, this is me being BODY POSITIVE. #line:982c15
	Mae: READ THE INTERNET!!!!	 #line:902359
<<elseif $eat_everything_i_see>>
	Mae: I am gonna get in shape and kick all asses! #line:552668
    Mae: I'm gonna eat everyone! Everyone I see! #line:552bdf
<<endif>>
Bea: Mae, I think you need to- #line:fb2482
<<if $maes_the_best>>
	Gregg: EFF EVERYONE! MAE'S THE BEST! #line:dea041
	Angus: I agree! But maybe you should pace yourself! #line:8c68cb
    Bea: I'm never driving any of you anywhere again. #line:a9041c
<<endif>>
Mae: EFF COLLEGE!!! #line:dd959d
Gregg: Yeah! #line:b64e86
Mae: ALL THOSE RICH KIDS AND HOT PEOPLE #line:b68dbd
<<dilate Mae 1 .5>>
<<lookRight Mae>>
Mae: ALL WRITING SEX AND HAVING PAPERS WITH EACH OTHER! #line:12868a
Angus: Close enough! #line:a18ce2
Mae: And that damn statue... pointing at me every day #line:1260c7
Mae: supposed to be the founder #line:12e919
Mae: :( #line:95bf65
Mae: but just shapes... #line:58873c
Mae: I... #line:b96de5
Mae: :| #line:f2eb43
<<lookUp Mae>>
Mae: I'M A TOTAL TRASH MAMMAL! #line:e7fdf1
Mae: AND NOTHING IS EVER GOING TO WORK OUT! #line:84cc34
Mae: we never had a chance here #line:2725d1
Mae: but i can't go anywhere else #line:7d3e62
Mae: ... #line:69e7f2
Mae: =_= #line:14b2b4
<<lookDown Mae>>
Mae: i'm home again #line:8cd309
Mae: and i can do something besides sleep #line:43aca7
Mae: and cry alone #line:5a23a8
Mae: *urp* #line:ad94c2
Mae: because things aren’t just things here, you know? #line:e3537e
Mae: oh god #line:23000b
Mae: i'm so #line:b4deca
Mae: -_- #line:9cc58e
Mae: :( #line:39e0ab
Mae: sick #line:6a47ff
Mae: to #line:008d95
Mae: death #line:2f6b3d
<<lookRight Mae>>
Mae: o_o #line:01a00b
Mae: :| #line:608a00
Mae: Does that make sense? Cole-io? #line:5b133d
Cole: Mae, I'm so sorry. I didn't mean to upset you. #line:ac5e3a
Cole: I think you’re a really great- #line:52284d
<<lookUp Mae>>
Mae: HOME AGAIN!!!! \o/ :) #line:b1f071
Gregg: WOOOO! #line:c011d7
Mae: WAAAAAAAAAAUGH :| #line:3a3605
<<close>>
<<swapRig Player MaePuke>>
<<trigger MaePuke Puke>>
<<wait 5>>
<<if $ate_tacos>>
	Mae: Tacos. #line:10f0df
<<endif>>
<<set $after_party to 1>>
<<fadeOutAmbientSFX AmbientSFX 1>>
<<fadeOutAmbientSFX AmbientSFX:Campfire 1>>
<<fadeOutAmbientSFX AmbientSFX:Walla 1>>
<<fadeOut 1>>
<<wait 1>>
<<scene BeaCar>>
===
title: Log
tags: 
colorID: 0
position: -6553,1064
---
<<sit>>
<<if $should_sit_log is 1>>
    [[ColeByFireEnding]]
<<else>>
    <<waitForInteractOrJump>>
<<endif>>
===
title: TestEnding
tags: 
colorID: 0
position: -6277,1111
---
<<close>>
<<playerJump>>
<<lookAt Mae Cole>>
<<wait 2>>
<<swapRig Player MaePuke>>
<<trigger MaePuke Puke>>
<<wait 5>>
===
