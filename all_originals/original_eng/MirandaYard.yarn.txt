title: EnterLevel
tags: 
colorID: 0
position: 197,193
---
<<if $miranda_yard is 0>>
     [[Dialogue1]]
<<else>>
    [[Dialogue3]]
<<endif>>
===
title: Dialogue1
tags: 
colorID: 3
position: 520,253
---
<<playMusic miranda_yard_intro>>
<<cameraLookAt IntroFocus0>>
<<cameraLookAt IntroFocus1 6>>
<<wait 6>>
Mae: couldn’t you just like refuse to do housecalls? #line:5ddbba
Bea: Nope. #line:ee0086
Mae: Why? #line:1093c0
Bea: It’s called a job. People pay you to do it. #line:053aa2
<<walk Bea BeaYard1>>
Bea: Ideally, at least. #line:f653d9
<<close>>
<<wait 1>>
<<look Bea Mae>>
Bea: You coming? #line:d3ad9b
<<close>>
Mae: There’s a thing back here by the driveway! #line:127947
Bea: Yeah. She’s an old lady. #line:346a4e
//look Bea right ?!?!} at other weird thing in yard//
Bea: She’s got weird shit in her yard. #line:2cd3a0
<<run Mae MaeYard1>>
Mae: It’s like a windmill! #line:84676a
Bea: Yep. #line:3129f8
<<close>>
<<waitForMove Bea>>
<<walk Bea BeaYard2>>
Mae: I’ve never been back up here before. #line:28d511
Bea: Mae Borowski, you have now been here. #line:25c7c9
<<cameraLookAt IntroFocus2 6>>
<<close>>
<<waitForMove Mae>>
<<walk Mae MaeYard2>>
Mae: It’s nice! #line:bcd23a
Bea: Yeah. Just a bunch of old cabins people turned into houses. #line:2ce662
Bea: Some hunting camps. #line:8be1a3
<<close>>
<<waitForMove Bea>>
<<walk Bea BeaPorch1>>
Bea: So don’t like get shot or anything. #line:caf859
Mae: Buck season isn’t for a few days, right? #line:295c47
<<close>>
<<waitForMove Mae>>
<<walk Mae MaePorch1>>
Bea: Well, #line:bf49a9
Bea: folks get impatient. #line:ae8f85
Mae: For [wave]muuuuuurder[/wave]. #line:d18377
Bea: It ain’t murder if it’s animals. #line:1f974c
Mae: That’s… dark. #line:f68cb1
Bea: Hold that thought. #line:5b5ec3
<<close>>
[[Dialogue2]]
===
title: Dialogue2
tags: 
colorID: 3
position: 842,260
---
Mae: You gonna knock? #line:e3f2e4
<<close>>
<<playAmbientSFX _AmbientSFX:door_knock>>
<<wait 1.5>>
<<irisOut 1 wait>>
<<set $miranda_yard to 1>>
<<scene MirandaHouse>>
===
title: Dialogue3
tags: 
colorID: 3
position: 326,498
---
Mae: Well that was fun! #line:976f77
Bea: uh huh. #line:462402
Bea: ... #line:1a9445
Mae: you alright? #line:c76fcb
Mae: she gave us lemonade! \o/ #line:8c44b5
Bea: yeah. #line:e8fff1
    ->you not like lemonade? #line:763eb5
        Bea: it's faaaaantastic. #line:4b72d6
    ->it's such a nice night out! #line:135e0f
        Bea: lovely. #line:5c37de
Mae: then what's up? _o_ #line:8298eb
Bea: nothing. just #line:f3a0e9
Bea: tired. #line:24a87b
Bea: worked all day. #line:0e8cdc
Mae: i'm all hyper! #line:681eb4
Bea: good for you. #line:f52d63
Mae: ugh. you are zero fun. #line:8d0cc9
Bea: yup. #line:49d246
Mae: you need like #line:8c2754
Mae: some magical shit #line:edf6c2
Mae: to like, give you a new perspective. #line:a278f5
Bea: let me know when that happens. #line:0b49c6
Bea: wouldn't want to miss it. #line:2854c0
<<playMusic fireflies_on_the_porch 10>>
===
title: GotFireflies
tags: 
colorID: 3
position: 810,51
---
<<freezePlayer>>
<<playerFace 1>>
<<playAmbientSFX _AmbientSFX:firefly_deliver>>
Bea: Wow. #line:b34df5
Bea: ha ha ha #line:2c1f23
Bea: are you like a firefly whisperer or something? #line:e0ce60
Mae: they just like me i guess! #line:c83ada
Bea: that's... #line:0c8cbd
Bea: You're an interesting person, Mae Borowski. #line:5cab60
    ->No, Mrs. Miranda is an interesting person. #line:728f12
        Mae: she like mummified a dude #line:f773d5
        Bea: ok, you're a more benign interesting. #line:b534fd
    ->Granddad said being interesting is all you can hope to be. #line:3e446e
        Bea: well, mission accomplished.  #line:2147be
Mae: woohoo. #line:5ee462
<<wait 2>>
Bea: you know #line:8d8999
Bea: i have to say this would have been a much less exciting evening #line:f7e502
Bea: if you weren't along for the ride #line:6cfa83
    ->along for the ride?! #line:a08eea
        Mae: i believe i worked pretty hard down there. #line:baeb43
        Mae: i should get a cut of the bill. #line:157702
        Bea: not a chance. you're an unpaid intern. #line:da54f2
        Mae: fight you for it! right here and now! #line:609a5c
    ->see? i'm good to have around. #line:9b6778
        Bea: i mean you can certainly beat the shit out of a furnace. #line:aa4d76
        Mae: i can beat the shit out of anything. #line:9840ba
Bea: you should like #line:a64fde
Bea: channel that aggression you always have #line:1acf6a
Bea: into something useful. #line:572af4
Mae: eh. Dr. Hank said way back i needed to repress it. #line:b96221
Bea: "repress"? not like "learn to deal with it"? #line:4476ae
Mae: he specifically said "repress". #line:d5c506
Bea: uh. ok. hm. #line:6a655d
<<wait 2>>
Mae: so do you think Mrs. Miranda like #line:6d6800
Mae: pulled her husband's guts out? #line:4179ee
Mae: isn't that what you do for mummies? #line:8c25b0
Bea: i dunno, mae. #line:1dcf42
<<close>>
<<cameraLookAt CameraDriftAway 10>>
//start drifting camera up into the skies
Mae: do you think you'd have the stomach for it? #line:67464f
Bea: nope. #line:9b50a7
Mae: it probably took a lot of heart. #line:ab7655
Bea: yep. #line:20c517
Mae: i wish she would ex-spleen it to us #line:69b1f7
Bea: i get it, mae. #line:b6b2ae
Mae: she really rectum! #line:d8e3c4
Bea: ok that probably stayed in place #line:1df370
Mae: yeah that should stay where it be-lungs! #line:8b2515
<<setAnimInt sitting Bea 0>>
<<warp Bea BeaSit>>
<<walk Bea WalkOffLeft>>
//walk bea off to the left//
Bea: i'm leaving #line:3e6beb
Mae: hey, you gotta liver your life! #line:0360bd
Bea: bye. this is me gone. #line:afa87a
<<walk Mae WalkOffLeft>>
//walk mae off to the left//
Mae: hey! wait up! #line:da951b
Bea: you're walking back to town #line:fd4860
Mae: you've got a lot of gall to say that! #line:201245
Bea: i'm calling the cops. #line:a9435c
<<wait 3>>
<<fadeOut 1 wait>>
<<unlockDoodle BeaFQ2>>
<<set $night to 1>>
<<set $did_bea_friendship_quest_2 to 1>>
<<set $completed_bea_friendship_quests += 1>>
<<scene MaeHouse left>>
===
title: InitLevel
tags: 
colorID: 0
position: -85,83
---
<<if $miranda_yard>>
    <<warp Bea BeaSit>>
    <<sit Bea>>
    <<flip Bea -1>>
    <<warp Mae Sit>>
    <<playerFace -1>>
    <<setCanInteract Bea 0>>
<<endif>>
===
