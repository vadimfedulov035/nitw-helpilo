title: EnterLevel
tags: 
colorID: 0
position: -90,111
---
<<if $gregg_friendship_quest_2 < 2>>
    <<set $gregg_friendship_quest_2 to 1>>
    [[GreggFQ2Intro]]
<<elseif $gregg_friendship_quest_2 is 2>>
    [[AfterDisconnect]]
<<endif>>
===
title: GreggFQ2Intro
tags: 
colorID: 0
position: 170,129
---
<<playMusic gregg_manic>>
<<walk Gregg GreggEnter>>
<<walk Mae MaeEnter>>
//walking across to the car, which is on the right//
Mae: rain sure cleared up. #line:622cf7
Gregg: weather moves for us. #line:dbede0
<<lookAt Mae Gregg>>
Mae: So what do I need the bat for? #line:859413
<<close>>
<<waitForMove Mae>>
<<waitForMove Gregg>>
<<lookAt Mae Gregg>>
<<wait 2>>
//they stop in front of the car//
<<lookRight Mae>>
Gregg: Ta-Da!!!! #line:83fa25
Mae: Ooh la la! #line:001074
Gregg: Eff that car up, Private Borowski! #line:bb547f
<<lookAt Mae Gregg>>
Mae: so... #line:a742ba
-> you brought me out here to watch me beat up a car? #line:dfc5d4
	Mae: Is this like that thing? Where people are all hot for car crashes? #line:38f542
	Gregg: No. And I don’t know that movie #line:a2f1cf
	Mae: Look it up. #line:0a73c5
	Gregg: eh. #line:0dc541
->why? #line:f1329d
	Gregg: do you need a reason? #line:579793
	Mae: No, but I’d like one. #line:2d8c6c
	Gregg: Fair. #line:ccc9d9
Gregg: So we need the battery out of this car. #line:faa107
Mae: What for? #line:47b7dd
Gregg: That’s privileged info, Private. #line:0d6a91
Mae: Stop calling me private. #line:1f031e
-> I’m a general at least #line:2cb846
	Gregg: No, *I’M* the general. #line:8fce2d
	Gregg: I got a hat. #line:f6591a
	Mae: War is stupid. #line:7fba3b
	Gregg: Watch it! War got me this hat! #line:23d325
-> I ain’t in your army. #line:7b1009
	Gregg: You’ve been drafted. #line:9c971f
	Mae: That’s, like, very unjust. #line:a13243
	Gregg: The world ain’t just, Private Borowski. #line:bd33ac
<<lookRight Mae>>
Mae: ok, but what if someone needs this car? #line:a5b1c8
Gregg: I think it’s safe to say no one’s gonna miss it. #line:f37615
Mae: yeah. I guess it’s got a tree growing through it. #line:739f17
<<lookAt Mae Gregg>>
->So we’re just beating it until the hood pops open? #line:861af1
->Why aren’t you helping? #line:0b30f1
	Gregg: I’m carrying the battery all the way back! #line:5bd7c9
	Gregg: Division of labor! #line:b0ca7d
	Gregg: You get the fun part anyways. #line:054d6d
	Mae: True! #line:cdb784
Gregg: Yeah! Just smash it up! #line:63b606
<<lookRight Mae>>
Mae: Smash it all up! #line:7352ba
Gregg: Smash capitalism! #line:428af8
Mae: Smash the government! #line:d8e528
Gregg: Smash this car! #line:18c6a4
Mae: YEEEAAAAARGH!!!! #line:387bbb
<<lookNearest Mae>>
<<set $gregg_angus_parking_lot_block_left to 1>>
===
title: CarDestroyed
tags: 
colorID: 0
position: 2704,105
---
<<fadeOut .1 wait>>
<<playerUpdateFallDuringInteract 1>>
<<warp Mae MaeStandForGregg>>
<<flip Mae 1>>
<<fadeIn .5 wait>>
Gregg: Excellent work. #line:747afe
Mae: Thank you. I agree. #line:df728d
Gregg: You want to do the honors? #line:0cb5b8
Mae: Am I gonna get electrocuted? #line:7920c1
Gregg: Should be fine. #line:88bc0c
Gregg: The car’s got a tree growing through it. #line:2f4686
Gregg: Probably not electric anymore. #line:24e99b
Mae: Wait. But... #line:abe254
Gregg: Do you want to do it or not? #line:e1f70f
Mae: ... #line:4aa9d3
Mae: I do. #line:173d07
<<close>>
<<set $has_bat to 0>>
<<irisOut 1 wait>>
<<scene PawsModeBatteryDisconnect>>
===
title: AfterDisconnect
tags: 
colorID: 0
position: 211,397
---
Mae: =_= #line:e71cee
//mae is on the ground, gregg is standing on top of the car//
Gregg: HA HA HA HA HA HA HA HA HA HA #line:4c1fb3
Mae: Uuuuugh. #line:8f3af9
Gregg: Oh shit #line:043fe9
Gregg: you ok? #line:1ebd5f
->I think I died #line:061076
->No #line:229227
Mae: How long was I out? #line:4b80d5
Gregg: Like 5 seconds? #line:457f2d
Mae: oh weird #line:2f52b8
Mae: Time, like, stretches. #line:8458d4
Gregg: It’s a mysterious universe. #line:02944f
Mae: We good to go with the battery? #line:fbbfca
Gregg: You know it! #line:7cc1ee
Gregg: Let's get back to the apartment! #line:c77c6b
Mae: ...I taste pennies... #line:2ce982
<<set $hasBat to 0>>
<<irisOut 1 wait>>
<<scene GreggAngusApartment>>
===
title: InitLevel
tags: 
colorID: 0
position: -347,90
---
<<if $gregg_friendship_quest_2 < 2>>
    <<getBat>>
    <<warp Gregg GreggOffLeft>>
    <<warp Mae MaeOffLeft>>
    <<set $gregg_angus_parking_lot_block_left to 0>>
<<else>>
    <<warp Gregg GreggTopOfCar>>
    <<flip Gregg -1>>
    <<setAnimInt Mae layingSide 1>>
<<endif>>
<<set $gregg_hat to 1>>
<<setCanInteract Gregg 0>>
===
title: CarDamage00
tags: 
colorID: 0
position: 422,87
---
Gregg: YEAH! DESTROY THE FASCIST CAR! #line:e401d7
===
title: CarDamage01
tags: 
colorID: 0
position: 688,69
---
Gregg: ONWARD! REVOLUTION! #line:9a17d6
===
title: CarDamage02
tags: 
colorID: 0
position: 929,99
---
Gregg: YEEEEAAAAAAAAAARGH!!! #line:dc7826
===
title: CarDamage03
tags: 
colorID: 0
position: 1164,134
---
Gregg: YOUR TIME HAS COME, CAR!! #line:53d31e
===
title: CarDamage04
tags: 
colorID: 0
position: 1398,87
---
Gregg: SAY YOUR PRAYERS, CAR! #line:624965
Gregg: TO WHATEVER GOD CARS PRAY TO! #line:575271
===
title: CarDamage05
tags: 
colorID: 0
position: 1632,119
---
Gregg: GHOST TIGER VAMPIRES RISE FROM THE MOON!!! #line:81de1b
//Mae: What?
//Gregg: I dunno! I go where the moment takes me!
===
title: CarDamage06
tags: 
colorID: 0
position: 1883,94
---
Gregg: BLOOD!!! GIMME BLOODDDDD!!!!! #line:a047ae
===
title: CarDamage07
tags: 
colorID: 0
position: 2114,131
---
Gregg: THE FINAL BLOW!!! #line:171d6c
===
title: CarDamage08
tags: 
colorID: 0
position: 2362,77
---
Gregg: DESTROOOOOOOOOOOOY!!! #line:23765e
===
