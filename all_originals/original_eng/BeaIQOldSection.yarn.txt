title: EnterLevel
tags: 
colorID: 4
position: -293,612
---
<<if $did_bea_coffin is 0>>
    <<playMusic graveyard_old_section>>
    [[Intro]]
<<else>>
    [[InSinkhole2]]
<<endif>>
===
title: Intro
tags: 
colorID: 4
position: -34,683
---
Bea: So like #line:8b612d
Bea: ghosts. #line:8b4301
Mae: ghosts? #line:e3ece4
Bea: like i dunno if i believe in them but #line:8de41b
Mae: yeah? #line:87db0e
Bea: So you know how you said you’ve been seeing ghosts at night? #line:10e1f7
Bea: I mean I don’t super believe it's not just dreams. #line:59b9cc
Bea: because obviously it's dreams. #line:fc5181
Mae: ok that's certainly your take on it. #line:e71220
Bea: but like... #line:2b91ee
Bea: did you ever see my mom? #line:0eed6c
->yes #line:9ac953
	Mae: it's hard to like recognize anyone #line:c41434
    Mae: but you can get a sense of people #line:dc7428
    Mae: and i uh i'm pretty sure i saw her #line:70e863
    Bea: ... #line:4b6332
    Bea: really? #line:1b0f6d
    Mae: as far as i can tell. #line:a36e49
    Bea: I just... want to know she's somewhere #line:8d5f34
    Bea: and ok. #line:4c8e73
    Mae: she's somewhere, bea. and ok. #line:c965d3
    Bea: thanks. i don't know if you're just lying #line:c31602
    Bea: or you dreamed it #line:509674
    Bea: or what. #line:a83996
    Bea: but... even if you just made all that up #line:0e8df7
    Bea: it was nice of you. #line:bb6304
    <<wait 2>>
->no #line:2b1572
	Mae: i mean, i can't really tell? #line:7e3881
    Mae: and i don't think the ghosts were like #line:9953bf
    Mae: people? #line:1c833a
    Mae: like they're not sitting around being ghosts. #line:51a588
    Mae: it's more like #line:380e7f
    Mae: echoes? #line:7d4840
    Mae: echoes of shit that happened and people that were here? #line:a4fe9b
    Bea: ... #line:66eb65
    Bea: oh. #line:dd8b5f
    Mae: trust me, none of the places looked like super much fun. #line:39b087
    Mae: your mom was rad as hell #line:5e0acf
    Mae: too rad to hang there. #line:5e00c5
    Bea: ha ha. that's like... #line:5f826b
    Bea: i appreciate you trying to turn this around. #line:8837c1
    Mae: i'm doing my best, beatrice. #line:4a6583
Bea: alright anyway #line:74d388
Bea: so the bad news is that we're locked out. #line:ccb878
Bea: by that big-ass gate. #line:aea9dd
Mae: ugh why make a giant gate in a graveyard? #line:87762c
Bea: um to keep people like you out #line:8299a2
Bea: and stop what you're probably gonna do. #line:456d4c
Mae: hm. yeah we'll never know for sure. #line:444fa8
Bea: alright well i'm not climbing it. #line:e05635
Bea: go do your thing. #line:06b5b2
Mae: my thing? #line:2d0426
Bea: you're good at this kinda thing! #line:351ab8
Bea: brute force and shit. #line:484bba
Mae: geez. gotta do everything around here. #line:080755
Bea: it's your ghost! i'm just the driver. #line:c1fb59
Mae: fine. hang out. i'll be back. #line:c82cfe
<<walk Bea BeaByGate>>
<<setCanInteract Bea 0>>
===
title: GateBreak
tags: 
colorID: 4
position: 336,500
---
<<freezePlayer>>
<<playerCanRun 0>>
<<playerCanJump 0>>
Bea: Uh. #line:935670
Bea: I sure hope nobody saw that. #line:e5aa85
Bea: We’re gonna get sued. #line:794d17
Mae: I don’t think anyone’s back here. #line:557ef2
Mae: And it’s getting late. #line:db814b
Bea: mmhm. #line:62fdc1
Mae: You’re welcome, by the way! #line:ca8ee5
Bea: Yeah see? I told you you're good at this. #line:d9ec66
Mae: my back hurts. #line:d6deed
Bea: so what do we need to do? #line:1d8113
Mae: uh I'm gonna go find Little Joe's grave #line:8616d7
Mae: and if he doesn't show up #line:5d28ce
Mae: i'm gonna knock on it until he does. #line:fee8c8
Bea: you're not allowed to destroy anything, mae. #line:3737dc
Mae: ok fine! whatever! let's go! #line:ed5863
<<set $at_joes_grave_done to 0>>
<<walk Bea BeaInPit>>
===
title: AtJoesGrave
tags: 
colorID: 4
position: 637,541
---
Bea: "Joe Shade" #line:bd0ea8
Bea: Yeah, this is him. #line:43ebdd
Bea: Ground here’s all swampy. #line:052773
Mae: Ugh. Gross. #line:0228ef
Bea: So what do we do here? #line:54924a
Mae: Um. I guess I'm gonna just look around? #line:f65d8a
Bea: for... ghosts? #line:a1ec97
Mae: ugh don't make this a thing. #line:20c369
Bea: it's not a thing! it's cool! #line:8bf454
Bea: mae, i am here for *you* right now. #line:75f5b8
Mae: sorry. you had a tone. #line:39e8d9
Bea: tone stowed. #line:b8fd8a
Mae: thank you. #line:15cd88
<<set $at_joes_grave_done to 1>>
<<set $check_old_section_1_done to 0>>
<<set $check_old_section_2_done to 0>>
<<set $check_old_section_3_done to 0>>
===
title: InitLevel
tags: 
colorID: 4
position: -628,593
---
<<setCanInteract Bea 0>>
<<set $check_old_section_1_done to 1>>
<<set $check_old_section_2_done to 1>>
<<set $check_old_section_3_done to 1>>
<<set $at_joes_grave_done to 1>>

<<if $did_bea_coffin>>
    <<warp Mae MaeInPit>>
    <<warp Bea BeaInPit>>
    <<flip Mae -1>>
    <<cameraLookAt CameraEnd>>
    //<<hide Collision:Ground>>
<<else>>
    //<<hide Collision:Ground2>>
<<endif>>

<<setSpriteLayer Bea CharacterFG>>
===
title: CheckOldSection1
tags: 
colorID: 4
position: 934,583
---
Mae: I found some old stuff! #line:24a161
Bea: what kind of old stuff? #line:02f3c6
Mae: graves? #line:34fc85
Bea: wow. #line:9cf745
<<close>>
<<set $check_old_section_1_done to 1>>
<<set $checked_old_section_count += 1>>
[[CheckOldSectionCount]]
===
title: CheckOldSection2
tags: temp
colorID: 4
position: 1189,643
---
Mae: This grave is for a horse I think? #line:a4f00b
Bea: I don't think they ever buried horses in people graves. #line:155e60
Mae: imagine the big horse-sized coffin. #line:a65a17
Bea: imagine. #line:786049
<<close>>
<<set $check_old_section_2_done to 1>>
<<set $checked_old_section_count += 1>>
[[CheckOldSectionCount]]
===
title: CheckOldSection3
tags: 
colorID: 4
position: 1469,608
---
Bea: any ghosts over there? #line:e4b3ae
Mae: no, bea. #line:0a0378
Mae: you'd know. #line:acb676
Mae: I'd say. #line:e767c1
Bea: ha ha ok ok #line:aa44a2
<<set $check_old_section_3_done to 1>>
<<set $checked_old_section_count += 1>>
[[CheckOldSectionCount]]
===
title: CheckOldSectionCount
tags: 
colorID: 4
position: 1206,940
---
<<if $checked_old_section_count >= 3>>
    <<setCanInteract Bea 1>>
<<endif>>
===
title: Bea
tags: 
colorID: 4
position: 1163,1206
---
<<wait 2>>
Mae: THIS IS STUPID! #line:f2cc12
Bea: What were you expecting? #line:d7caef
Mae: SOMETHING! #line:69f519
Mae: It’s just... STUPID!!! #line:fc82cb
Mae: HE DIDN’T POP OUT OF THE GROUND OR ANYTHING! #line:e8447d
Bea: Is... is that what you were expecting to happen? #line:4566df
Mae: THIS IS STUPID! #line:a91e59
Mae: I’m gonna smash his shit up! #line:195d4e
Bea: uh whoa dude hold up no #line:6ff57e
Mae: [shake=.05]WAKEY WAKEY LITTLE JOE!!![/shake] #line:f12eda
<<playerCanRun 1>>
<<playerCanJump 1>>
<<setCanInteract Bea 0>>
===
title: JumpOnJoeGrave
tags: 
colorID: 4
position: 1478,1210
---
Bea: ok. that didn’t sound right. #line:432447
===
title: InSinkhole
tags: 
colorID: 4
position: 1748,1204
---
//<<hide Collision:Ground>>
//<<show Collision:Ground2>>
<<warp Bea BeaSinkhole>>
<<warp Mae MaeSinkhole>>
<<flip Mae -1>>
<<freezePlayer>>
Bea: HOLY SHIT! #line:05de66
Mae: HOLY SHIT! #line:6d690f
Bea: WHAT DID YOU DO??? #line:d1cf74
Mae: You were right! #line:f169d8
Bea: About what??? I didn’t tell you to do that? #line:c4e71b
Mae: About the sinkholes! #line:ed58c2
Bea: We’re lucky it wasn’t deeper. #line:df893a
Bea: Wait... is that... #line:a19871
Mae: Yeah. That’s a coffin. #line:aa0037
Bea: Oh crap. #line:b4c15d
Mae: So, we're gonna... open it... right? #line:b54b04
Bea: no. we're not going to open a coffin #line:d3eb27
Bea: with a rotting corpse in it. #line:e8848d
Mae: Bea, it's been a long time. #line:e91f91
Mae: he's not rotting anymore. #line:b838c3
    <<if $did_bea_friendship_quest_2>>
        Mae: we got more a Mr. Miranda situation going on here. #line:e98a67
    <<endif>>
Bea: geez god we are not doing this we are leaving #line:90ddc4
Mae: aren't you kinda curious at least? #line:74f819
Bea: ... #line:d85af9
Bea: ... #line:39e865
Bea: ...yeah. #line:5870fd
Mae: ok! let's do this! #line:1ac12d
Bea: you're doing it. I'm standing here. #line:aa4da7
<<close>>
<<irisOut 1 wait>>
<<scene PawsModeBeaCoffin>>
===
title: InSinkhole2
tags: 
colorID: 4
position: 2004,1203
---
Mae: so what, foul play? #line:039178
Bea: who knows. #line:ca21a7
Mae: we're detectives! #line:b6ce50
Bea: we're not. we're like #line:8f7189
Bea: criminal vandals #line:8db13b
Bea: or like graverobbers or something. #line:f481f9
Mae: those too! #line:0fdf30
Mae: we're a lot of things right now. #line:fc7039
Bea: SHH! #line:b4abe3
Mae: what? #line:11c4ec
<<playAmbientSFX _AmbientSFX:amb_ibon 3>>
<<wait 2>>
Bea: So there’s someone back there watching us. #line:f1d83e
Mae: Oh god... #line:9718df
Bea: i don't know where they went #line:3786fc
Bea: just like #line:438eec
Bea: out of the corner of my eye... #line:b4f03e
Mae: is it... #line:b3792e
Bea: I don’t know. But we need to leave. NOW! #line:f75aec
Mae: it could be the ghost! #line:2a7a3e
Bea: NO! We're gone! Now! #line:0b1e04
Mae: ok fine geez! #line:3d678a
<<set $graveyard_branch_break to 1>>
<<close>>
<<run Mae OffLeft>>
<<run Bea OffLeft>>
<<wait 2>>
<<trigger GraveyardEide Run>>
<<wait 4>>
<<set $want_graveyard_ending to 1>>
<<fadeOut 1 wait>>
<<scene BeaIQGraveyard left>>
===
title: NeedBea
tags: 
colorID: 4
position: 455,1133
---
<<if $graveyard_need_bea is 0>>
    Mae: There's no way I'm going to leave Bea behind... #line:f58345
    Mae: Ugh Beatrice. #line:3d810d
    Mae: just learn to jump. #line:03dfc8
    Mae: not with those scrawny legs I guess. #line:81708b
    Mae: how do I break that gate open? #line:136339
    <<set $graveyard_need_bea to 1>>
<<elseif $graveyard_need_bea is 1>>
    Mae: Hmm, there's gotta be a way to break that gate open... #line:300bba
<<endif>>
===
