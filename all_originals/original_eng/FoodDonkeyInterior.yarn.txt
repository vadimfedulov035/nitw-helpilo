title: EnterLevel
tags: 
colorID: 3
position: -1545,1363
---
<<if $act is 2 and $day is 1>>
    <<if $gregg_friendship_quest is 1>>
        <<show Gregg>>
        <<show SteveScriggins>>
        <<setCanInteract Gregg 0>>
        <<setCanInteract SteveScriggins 0>>
        <<walk Gregg GreggWalkIn>>
        <<walk Mae MaeWalkIn wait>>
        <<playerCanRun 0>>
        <<playerCanJump 0>>
        [[Intro]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    [[LastDayScene]]
<<else>>
    <<set $wont_open_food_donkey_basement_door_1 to 0>>
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: -1618,1131
---
<<hide Gregg>>
<<hide SteveScriggins>>

<<set $arcade_machine_1_done to 1>>
<<set $arcade_machine_2_done to 1>>
<<set $arcade_machine_3_done to 1>>
===
title: PickRobotMascot0
tags: 
colorID: 3
position: -867,1849
---
Gregg: Aw yeah. Froggy robot. #line:cf2c6f
SteveScriggins: Finally. Goddamn. #line:ad000b
Mae: Oh shut up Steve. #line:3c7574
<<set $robot_head to 0>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot1
tags: 
colorID: 3
position: -600,1853
---
Gregg: Yeah! Robot piggy! #line:0f71f1
SteveScriggins: Finally. Goddamn. #line:6d10f5
Mae: Oh shut up Steve. #line:55c353
<<set $robot_head to 1>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot2
tags: 
colorID: 3
position: -322,1856
---
Gregg: Cool cool. Mecha-bunn. #line:dbed46
SteveScriggins: Finally. Goddamn. #line:5f8904
Mae: Oh shut up Steve. #line:003392

<<set $robot_head to 2>>
[[ArcadeMachinePicked]]
===
title: Gregg
tags: 
colorID: 3
position: -653,933
---
<<if $gregg_friendship_quest_1 is 0>>
    <<stopDialogue Gregg>>
    [[CowHeadConvo]]
<<elseif $gregg_friendship_quest_1 is 1>>
    <<if $food_donkey_key_ring > 0>>
        <<stopDialogue Gregg>>
        [[DoorUnlock]]
    <<elseif $gregg_fq1_look is 0>>
        <<stopDialogue Gregg>>
        [[DoorLocked]]
    <<elseif $gregg_fq1_look is 1>>
        <<set $gregg_fq1_look to 2>>
        [[GreggLook1]]
    <<elseif $gregg_fq1_look is 2>>
        <<set $gregg_fq1_look to 3>>
        <<setCanInteract Gregg 0>>
        [[GreggLook2]]
    <<endif>>
<<elseif $gregg_friendship_quest_1 is 2>>
    <<stopDialogue Gregg>>
    Mae: Oh god. These guys. #line:03fc35
    Mae: I was both terrified and super into these things #line:be955e
    Mae: when they'd put them out front. #line:f18682
    Mae: They moved! #line:74ff84
    Gregg: They look so tiny when they're in pieces. #line:7c8bc4
        ->I dunno man they're still pretty big. #line:c44eba
            Mae: God, these things were actually gigantic when they had them out. #line:9dfdc5
        ->We're smaller in pieces too. #line:1f9746
            Gregg: Too real. #line:ab0e9f
            Gregg: I hope you get chopped up by a chopper. #line:c82c72
            Mae: I hope you get chopped up by a blunt chopper. #line:edc300
            SteveScriggins: You two are weird. #line:face10
    Gregg: ok, we're stealing one of these #line:d45b76
    Mae: is it even stealing? #line:4f805d
    Gregg: it's better if we think of it as stealing. #line:907dd1
    Mae: cool #line:70900d
    <<close>>
    <<wait .5>>
    Gregg: aaaagh. #line:dcc89d
        -> aaagh? #line:699a9e
        -> what? #line:e18767
    Gregg: aaaaaaaaaagh. #line:b35cc3
        -> are you ok? #line:9ae45d
        -> dude, what? #line:2b4fb7
    Gregg: {width=300}aaaaaaaaaaaaaaaaaaaaaaaaaagh!!! #line:3af4e2
        -> DUDE! WHAT! #line:9c6b89
        -> STOP SCREAMING!!! #line:2a09bd
    Gregg: I can't pick! They all look great! #line:6939b7
    SteveScriggins: You get one. That's the deal. Hurry up. #line:9f4197
    Mae: Oooh let me pick! Let me pick! :) #line:8b2667
    <<close>>
    <<run Gregg Gregg3 -1 wait>>
    Gregg: sure. #line:4a3753
    Mae: :| #line:83366f
    <<set $gregg_friendship_quest_1 to 3>>
    <<set $robot_head_choice_enabled to 1>>
<<elseif $gregg_friendship_quest_1 is 3>>
    <<if $gregg_fq1_try_decide is 0>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide00]]
    <<elseif $gregg_fq1_try_decide is 1>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide01]]
    <<elseif $gregg_fq1_try_decide is 2>>
        [[TryToDecide02]]
    <<endif>>
<<endif>>

===
title: ArcadeMachinePicked
tags: 
colorID: 3
position: -607,2238
---
<<close>>
<<wait 1>>
SteveScriggins: You know what? I ain't carrying nothin'. #line:539a13
SteveScriggins: See ya ladies. #line:3d5f3d
<<close>>
<<walk SteveScriggins SteveScriggins1>>
Mae: Steeeeeeeve!? #line:e0a430
SteveScriggins: Get found dead, punks! #line:67a6f7
SteveScriggins: Hweh heh heh. #line:8db583
<<close>>
<<wait 2>>
<<hide SteveScriggins>>
Gregg: He says punks like it's a bad thing. #line:1e46a1
Mae: I don't care. #line:c2da95
Mae: He's scum and a criminal and anyway I think he's been bad news from the start. #line:9f6b9b
Gregg: Mm. Yes. #line:99b7af
Mae: What deal did you make with him? #line:c3b499
Gregg: He's allowed to shoplift whatever he wants from the Snack Falcon while I'm working. #line:d25707
Mae: See? Criminal all the way. #line:c21299
Gregg: Some people... #line:93ec64
<<close>>
<<wait 1>>
Mae: So should we like smash a window to get this out of here? #line:04f5b8
<<close>>
<<set $robot_crate to 1>>
<<irisOut 1 wait>>
<<scene CarryingRobotMascot>>
===
title: NotSure
tags: 
colorID: 3
position: -318,1575
---
Gregg: Let me know when you're ready. #line:fa0d15
===
title: Intro
tags: 
colorID: 3
position: -1896,1678
---
Mae: Oh god. What is that smell? #line:f6e088
SteveScriggins: Glue. #line:fad0f9
Mae: Oh no. Is that- #line:5d7fae
<<walk SteveScriggins SteveWalkIn>>
SteveScriggins: [wave]Sup ladies.[/wave] #line:abf7a2
Mae: =_= #line:9ee1e3
<<vo Mae dissapointed>>
Mae: ugh. :( #line:904858
Mae: Effing Steve Scriggins. #line:ec28b5
SteveScriggins: *FFFFFFFTTTTT!!* #line:fa65df
Mae: Did you just actually make a fart noise? #line:ac2c40
SteveScriggins: Hweh heh heh. #line:654f35
SteveScriggins: You ladies lost or something? #line:d8ac7e
Gregg: Deal's a deal, Steve. #line:83a07b
SteveScriggins: I ain't forgot. #line:551af9
Gregg: Where is it then? #line:4af5c7
SteveScriggins: In the back. #line:13520c
Mae: I remember you, you know. #line:250d22
SteveScriggins: I bet [wave]everyone[/wave] remembers you. #line:9470de
    ->That teacher sue you yet? #line:53c672
        Mae: Or at least beat you up in a parking lot? #line:74925f
        SteveScriggins: Which teacher? #line:b063c2
        Mae: The one you locked in the closet during Longest Night break. #line:1a29e0
        SteveScriggins: Pssh. That guy can't even go in small rooms now. #line:6cb47f
        SteveScriggins: He was always a wuss, just needed someone to bring it out. #line:c1cad9
        Mae: ... O_O #line:72372f
        Gregg: ... O_O #line:adb578
    ->What are you doing in here? #line:27d830
        SteveScriggins: Your mom. #line:3d5d01
        Mae: Great Steve. #line:cfe2ef
        SteveScriggins: All the ways. #line:1dd2cd
        Mae: Cool. #line:e105bb
        SteveScriggins: I'm your new dad. #line:d8d1c3
        Mae: Steve, you sincerely could not get with my mom. #line:976352
        SteveScriggins: Is that a challenge? #line:35ff2a
Mae: o_o :| #line:a25e73
Gregg: o_o #line:332935
Gregg: ok well #line:6e68be
Gregg: can we get on with this? #line:41c1e9
<<close>>
<<run SteveScriggins SteveScriggins1 -1>>
<<run Gregg Gregg1>>
<<wait 3>>
<<setCanInteract Gregg 1>>
<<runDialogue Gregg GreggScrigginsBG1>>

===
title: GetKeyRing
tags: 
colorID: 3
position: 264,380
---
<<stopDialogue Gregg>>
Mae: No wonder this place went under. #line:df2ec4
Mae: People leaving keys all over the place! #line:ab556f
Mae: Don't mind if I do. #line:f2ee16
<<fadeOutInternal 0>>
<<playOneShot event:/food_donkey/key_pickup Mae>>
<<wait 1>>
<<set $food_donkey_key_ring to 1>>
<<set $got_food_donkey_key_ring to 1>>
<<setPlayerInteractLocator Gregg MaeDoorConvo>>
<<warp Gregg GreggDoor2>>
<<flip Gregg 1>>
<<clearAltBodyRig Gregg>>
<<setAnimInt Gregg kickingDoor 1>>
<<setAnimInt SteveScriggins kickingDoor 1>>
<<lookRight SteveScriggins>>
<<warp Mae MaeFoundKey>>
<<flip Mae 1>>
<<freezePlayer>>
<<fadeInInternal 1 wait>>
-> Found some keys! #line:67715d
    <<close>>
	<<wait 2>>
	Mae: Guys? #line:3477ef
-> I am the keymaster! #line:42f95d
	Mae: I will unlock your ass! \o/  #line:0257c2
	SteveScriggins: Yeah I heard that about you! #line:b50905
Mae: _o_ #line:9aa625
<<setCanInteract Gregg 1>>
===
title: RobotMascot0
tags: 
colorID: 3
position: 118,1162
---
Mae: Ol' greenie here. #line:6dbf9c
Gregg: I used to have dreams about this thing. #line:5475d3
SteveScriggins: Sex dreams #line:8665a2
SteveScriggins: Probably. #line:9ea776
SteveScriggins: Best you could get. #line:3de628
SteveScriggins: Probably. #line:6e4c27
Mae: ... #line:6c1434
Gregg: Do frogs even have sex? #line:906358
Mae: Nah, they got like eggs or something. #line:49a952
SteveScriggins: ... #line:dcb6a8
SteveScriggins: You two are really, really dumb. #line:8e1c7a
<<set $robot_head_0_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot1
tags: 
colorID: 3
position: 408,1165
---
Mae: Pretty cool pig. #line:9bb4d4
Gregg: Yeah pretty alright. #line:39c073
Gregg: Didn't he used to hold like sausage links or something? #line:f12c6b
Mae: Yeah! That's messed up! #line:52eb57
SteveScriggins: Eatin' his own kind. #line:1c45c6
Mae: or her own kind. #line:dc9312
SteveScriggins: Whatever, PC-police. #line:cb2684
Mae: PC yourself into traffic Steve. #line:8c7d71
Gregg: ANYWAY. #line:41492b
<<close>>
<<wait .5>>
Mae: Cannibal pig, man. #line:4da150
Gregg: Cannibal pig. #line:b2bc85
<<set $robot_head_1_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot2
tags: 
colorID: 3
position: 655,1160
---
Mae: God. I remember these being less horrifying. #line:17cc92
Gregg: They were tall then. Couldn't see their faces. #line:a4e94e
SteveScriggins: I gotta look at you ladies' faces. #line:e34b39
SteveScriggins: Hweh heh heh. #line:b5421a
<<lookAt Mae SteveScriggins>>
Mae: -_- #line:a87102
Gregg: I think this one's kinda cute. #line:34ea19
SteveScriggins: Aw, he thinks it's cute. #line:0d2101
Mae: Steve you are a disease. #line:6d6a65
Mae: o_o #line:e5b1f0
<<set $robot_head_2_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: Checked2ndRobotMascot
tags: 
colorID: 3
position: 412,1539
---
SteveScriggins: GEEZ. Why does she get to pick? #line:6a5cbc
SteveScriggins: Why is this such a thing? #line:0cc1d4
SteveScriggins: This is taking forever! #line:d79109
Mae: :( \o/ #line:3ff5dc
Mae: [shake=.05]SCRIGGINS! BITE MY ENTIRE ASS![/shake] #line:e8e72d
SteveScriggins: WHAT?! #line:497d4d
Mae: [shake=.05]GO LEGALLY BRAIN DEAD[/shake] #line:7f2b56
Mae: [shake=.05]FROM LACK OF OXYGEN[/shake] #line:a31f92
Mae: [shake=.05]DUE TO CHOKING ON MY ENTIRE ASS![/shake] #line:6a859b
SteveScriggins: I... I... what? #line:2b4f1d
Gregg: Classic. #line:a59bd1
Mae: [shake=.05]THAT'S RIGHT, STEVE! CLASSIC![/shake] #line:58b520
SteveScriggins: [shake=.05]OH MY GOD CAN WE JUST GET THIS DONE ALREADY[/shake] #line:56728d
Mae: :| _o_ #line:e04c44
===
title: DecisionPoint
tags: 
colorID: 3
position: -610,1588
---
Mae: Ok kids we're gonna go with... #line:a60d14
[[Actually, I'm not sure yet...|NotSure]] #line:a56e50
<<if $robot_head_0_done is 1>>
    [[Frog Head|PickRobotMascot0]] #line:ba2903
<<endif>>
<<if $robot_head_1_done is 1>>
    [[Pig Head|PickRobotMascot1]] #line:a002bf
<<endif>>
<<if $robot_head_2_done is 1>>
    [[Rabbit Head|PickRobotMascot2]] #line:4b2b97
<<endif>>
===
title: TryToDecide00
tags: 
colorID: 3
position: -957,1288
---
Gregg: Ugh... which one? #line:66664a
Gregg: I'm bad at decisions that actually mean something. #line:69516a
Mae: You picked out the tuxes we wore to junior prom. #line:75da50
Gregg: Ok yeah... I'm pretty good at it. #line:73bfd9
Mae: Remember how Tim Whitley rode that giant tractor into homecoming? #line:4d04d5
Gregg: Totally. #line:bb0316
Gregg: So... did you pick? #line:3ca941
-> Dude gimme time. #line:e17c53
    Gregg: Ugh fine. #line:88a40e
    Mae: Dude, is this serious business? #line:c5db25
    Gregg: This is serious business. #line:372a1a
    Mae: Ok then let's treat it like serious business. #line:834257
    Gregg: I'm hungry though. #line:dc3eed
    Mae: Uuuugh me too. #line:f704b7
    Gregg: We should eat later. #line:23bfbd
    Mae: Yeah probably. #line:8ef300
    SteveScriggins: I DO HAVE places to be, you know!	 #line:622f69
-> I totally did. #line:bb5c3d
    Gregg: Oh cool #line:b1368f
    [[DecisionPoint]]
===
title: TryToDecide01
tags: 
colorID: 3
position: -621,1275
---
Gregg: Which one are we going with? #line:6a0429
-> [shake=.05]YOU GIVE ME A SEC.[/shake] #line:d2c879
    Mae: \o/ #line:2806f2
    <<dilate Mae .5 .25>>
    Gregg: DUDE. CHILL. #line:21f7d5
    <<dilate Mae 1 .5>>
    Mae: _o_ #line:15fa0d
-> Um... ok. I think I got it. #line:5d67bc
    Gregg: Sweeeeet. #line:a8057f
    [[DecisionPoint]]

===
title: TryToDecide02
tags: 
colorID: 3
position: -339,1279
---
Gregg: Anything? #line:5a0765
-> Nope! #line:c15a04
-> Yep! #line:c49d0a
    Gregg: YAY! #line:3d3e24
    [[DecisionPoint]]
===
title: GreggScrigginsBG1
tags: 
colorID: 3
position: -308,360
---
Gregg: {emote=hotdog} #line:0fd3c9
SteveScriggins: {emote=hotdog} #line:ee47fe
[[GreggScrigginsBG1]]
===
title: CowHeadConvo
tags: 
colorID: 3
position: -1149,1050
---
Gregg: So this still works sometimes? #line:94e75a
SteveScriggins: Eh. #line:c1f56a
SteveScriggins: You know Beth Holstead? #line:07041a
Gregg: Yeah. #line:7c6086
SteveScriggins: She was in here like 6 months ago and she swears it talked. #line:f4826c
Gregg: Whoa. #line:9901fa
SteveScriggins: Didn't even have to push the button. #line:8a8c34
SteveScriggins: But I dunno how much stake I’d put in it. #line:84f2c5
SteveScriggins: You know Beth. #line:ac3deb
Gregg: Kinda? #line:6800f9
SteveScriggins: There’s a lot of glue in here. #line:c3cf18
Gregg: I guess. #line:1a7b84
<<set $gregg_friendship_quest_1 to 1>>
<<setInteractDir Gregg -1 0>>
<<walk SteveScriggins SteveScrigginsDoor1 -1>>
<<walk Gregg GreggDoor1>>
<<runDialogue Gregg GreggScrigginsBG2>>
===
title: GreggScrigginsBG2
tags: 
colorID: 3
position: -72,373
---
<<waitForMove SteveScriggins>>
SteveScriggins: {emote=cloud_rain} #line:a4aa76
<<close>>
<<wait 1>>
[[GreggScrigginsBG2]]
===
title: DoorLocked
tags: 
colorID: 3
position: -1144,801
---
<<flip SteveScriggins -1>>
SteveScriggins: [shake=.05]GODDAMMIT!!![/shake] #line:f42503
Mae: What? #line:22597a
SteveScriggins: Someone locked the effin’ door! #line:7c7633
SteveScriggins: Goddammit I bet it was Beth. #line:5f6dd6
SteveScriggins: Or Levy. #line:bc8c87
SteveScriggins: Or the train trash. #line:171a13
Gregg: So... #line:433ee7
SteveScriggins: AAAAAAAAAAAARGH #line:08f274
SteveScriggins: They better not have stolen anything. #line:35fe96
Mae: ...from the abandoned grocery store... #line:78631e
Mae: ...with the unlocked doors… #line:08fe8e
Mae: What’s behind this door anyway? #line:414a78
SteveScriggins: Could probably bust it open... #line:53e27a
Gregg: Or I could go get my lockpick stuff. #line:5dc5cc
Mae: I bet there’s like a key or something. #line:a4ff55
SteveScriggins: Whatever. #line:60fbb0
<<flip SteveScriggins 1>>
<<setAnimInt SteveScriggins kickingDoor 1>>
<<close>>
<<run Gregg GreggLook1>>
<<wait 3>>
<<stopMoving Gregg>>
<<warp Gregg GreggLook1>>
<<setAltBodyRig Gregg GreggRummageRig>>
<<playerCanRun 1>>
<<playerCanJump 1>>
<<set $gregg_fq1_look to 1>>
===
title: GreggLook1
tags: 
colorID: 3
position: -1108,506
---
Mae: What are you doing? #line:7b4f3d
Gregg: Trying to find something to help bust down the door. #line:1cdf49
Mae: Cool. I’m looking for a key because I’m smarter. #line:7e1e18
Gregg: Uh huh. But I get to hit things. #line:78d044
Mae: ... #line:d79326
Mae: crap. #line:a242bf
===
title: GreggLook2
tags: 
colorID: 3
position: -817,445
---
Gregg: How much glue did they need in here? #line:c14ced
Mae: Dude you gotta stick stuff to stuff to run a successful business. #line:d53ad2
Gregg: Well, apparently not. #line:7aa431
Gregg: What are you doing? #line:2f3d51
Mae: Gonna check that office up there. #line:f5f106
Gregg: Cool. Don’t like slice yourself to death on the glass. #line:d7a334
Mae: Dumb death. #line:2c43c9
Gregg: Dumb death. #line:bdc63d
===
title: DoorUnlock
tags: temp
colorID: 3
position: -228,847
---
<<setPlayerInteractLocator Gregg>>
Mae: Yo idiots stop kicking before you break your femurs or something. #line:6ac77f
Gregg: I’m not an idiot. #line:9998a3
Mae: Yeah sorry hey idiot and Gregg stop kicking. #line:0d8b6d
Mae: I got keys. #line:7d4d5f
<<setAnimInt SteveScriggins kickingDoor 0>>
<<setAnimInt Gregg kickingDoor 0>>
<<walk Gregg GreggDoor1 1>>
<<flip SteveScriggins -1>>
<<lookNearest SteveScriggins>>
SteveScriggins: Where’d you get them? #line:21d50a
Mae: Office. Up there. #line:85ac05
SteveScriggins: No wonder this place went under. #line:dd9132
SteveScriggins: Leaving keys all over the place! #line:61ec2e
Mae: That was exactly what I thought. #line:081083
<<walk SteveScriggins SteveScrigginsDoor2 1>>
<<walk Mae GreggDoor2>>
Mae: I’m upset that we thought like the exact same words. #line:262410
SteveScriggins: I thought it better. #line:cc94e2
<<lookRight Mae>>
Mae:{emote=food_donkey_key} #line:e5558a
<<fadeOutMusic 5>>
<<close>>
<<playOneShot event:/food_donkey/key_door Mae>>
<<wait 1.1>>
<<playOneShot event:/food_donkey/door_open Mae>>
<<wait 1>>
<<tell BasementDoor Unlock>>
<<wait 1>>
<<walk SteveScriggins SteveScriggins2>>
<<wait 1.5>>
<<setInteractDir Gregg 1 0>>
<<lookNearestMae>>
->Why are we doing crimes with this guy? #line:5ee8c4
	Gregg: Robbers can’t be choosers. #line:fe058b
	<<walk Gregg Gregg2 -1>>
	Mae: They really can. #line:3b160d
	SteveScriggins: You know I can hear you, right? #line:a09ff1
->Ugh. Why did it have to be Steve? #line:49924c
	Gregg: He had an in. #line:3b3c2e
	<<walk Gregg Gregg2 -1>>
	Mae: What in? #line:e58cdf
	SteveScriggins: I broke the front door. #line:23f714
<<playMusic food_donkey_faces 3>>
<<set $gregg_friendship_quest_1 to 2>>
===
title: LastDayScene
tags: 
colorID: 6
position: -1420,1661
---
<<if $food_donkey_last_scene is 0>>
    <<set $food_donkey_last_scene to 1>>
    <<fadeIn 1>>
    <<warp Mae MaeMallardRats1>>
    <<run Mae MaeMallardRats2 wait>>
    <<lookLeft Mae>>
    Mae: :) \o/ [wave]My babies!!![/wave] #line:30eeae
    <<lookUp Mae>>
    Mae: My little rat babies... #line:4b21af
    <<lookNearest Mae>>
    Mae: Well, not so little anymore.  #line:45b100
    Mae: You've made it! You've broken free! #line:a1ab40
    Mae: U_U It's a [wave]miracle![/wave] #line:cd174f
    <<lookUp Mae>>
    Mae: _o_ I'm just gonna enjoy this moment for a bit. o_o #line:51e243
    Mae: Don't mind me... #line:610c8d
    <<fadeOut 2 wait>>
<<else>>
    <<fadeOut .0001 wait>>
<<endif>>
<<warp Mae MaeSitUpTop>>
<<sit3>>
<<lookNearest Mae>>
<<setSortingLayerName PlayerBG5>>
Mae: :) #line:a73b60
<<wait .5>>
<<fadeIn 2 wait>>
<<waitForInteractOrJump>>
<<fadeOut 2 wait>>
<<scene Outskirts left>>
===
