title: DanInitLevel
tags: 
colorID: 0
position: -379,-289
---
<<if $act is 0 and $day is 0 and $Underhill is 1>>
    <<show Dan>>
<<elseif $act is 1 and $Underhill is 1 and $talked_to_dan_in_act_1 is 0>>
	<<show Dan>>
    <<trigger Dan CoffeeIdle>>
<<elseif $act is 2 and $day is 1 and $OlPickaxe>>
	<<show Dan>>
<<elseif $act is 2 and $day is 3 and $OlPickaxe>>
	<<show Dan>>
<<elseif $act is 3 and $day is 1 and $TowneCentreWest>>
    <<warp Dan DanBehindDiner>>
    <<show Dan>>
<<elseif $act is 4 and $day is 3 and $TowneCentreWest>>
    <<show Dan>>
    <<trigger Dan TacoSignIdle>>
<<elseif $act is 2 and $day is 4 and $OlPickaxe is 1 and $night>>
	<<show Dan>>
<<else>>
	<<hide Dan>>
<<endif>>

===
title: Dan
tags: 
colorID: 0
position: 1,183
---
<<if $act is 0 and $day is 0>>
	[[DanDemo]]
<<elseif $act is 2 and $day is 1>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA2D1_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA2D1_01]]
    <<endif>>
<<elseif $act is 2 and $day is 3>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA2D3_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA2D3_01]]
    <<endif>>
<<elseif $act is 3 and $day is 1>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA3D1_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA3D1_01]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $dan is 0>>
        <<set $dan += 1>>
        <<if $met_dan is 0>>
            [[DanIntro]]
        <<else>>
            [[DanA4D3_00]]
        <<endif>>
    <<elseif $dan is 1>>
        <<set $dan_done to 1>>
        [[DanA4D3_01]]
    <<endif>>
<<elseif $act is 1>>
	<<if $dan is 0>>
        <<set $talked_to_dan_in_act_1 to 1>>
        <<set $dan += 1>>
		[[Dan_00_00]]
	<<elseif $dan is 1>>
        <<set $dan += 1>>
		<<set $dan_done to 1>>
		[[Dan_00_01]]
	<<endif>>
<<endif>>

===
title: Dan_00_00
tags: demo2
colorID: 0
position: 291,499
---
<<set $met_dan to 1>>
Mae: Danny, right? #line:1e31fb
Dan: Yep. #line:173b22
Mae: I think you were a senior when I was a freshman? #line:aa7c41
Dan: Probably. #line:975937
Mae: How’s it going? #line:8e6335
Dan: Just lost my job... #line:bb9b0e
Mae: Oh no! #line:34d338
Dan: They say construction’s always hiring #line:e85056
Dan: but it’s not. #line:f15d3a
Dan: In fact, it’s often laying off guys named Dan. #line:cf9f4e
Mae: What are you going to do? #line:0c4300
Dan: I’m open to suggestions. #line:394f7a
[[Don’t give up!|Dan_00_DontGiveUp]] #line:86a190
[[I’m sure *someone* in town is hiring!|Dan_00_Someone]] #line:61e2da
===
title: Dan_00_01
tags: demo2
colorID: 0
position: -47,468
---
Mae: I don’t have a job either. #line:4e663f
Dan: Aw man, sorry to talk your ear off about it. #line:cbb9f0
Dan: How are you holding up? #line:ea94b4
Mae: Good. Just dropped out of college and I’m living with my parents. #line:b07126
Dan: Oh. #line:e4bbc9
Dan: So you’re basically a teenager again. #line:a91e87
Mae: NO. #line:8cc110
Mae: I’m just an adult living with her parents. #line:7ab0a1
Dan: Oh ok so it’s not like you’re unemployed #line:a63fa1
Dan: it’s like you’re lacking a daytime hobby #line:dfad55
Dan: that pays money you don’t need for rent. #line:f6d174
Mae: Alright, I’m gonna get going. #line:541a24
Dan: Yeah. #line:58062b
===
title: DanDemo
tags: demo
colorID: 0
position: 465,171
---
<<if $dan is 0>>
	Dan: {width=2}Construction's always hiring, Rob! #line:7cdc05
	Dan: {width=2}Well, at least the coffee shop is still open. #line:43a93a
	Dan: {width=2}Even if it's not hiring. #line:6abeaf
	<<set $dan to 1>>
	<<set $dan_done to 1>>
<<endif>>

===
title: Dan_00_DontGiveUp
tags: demo2
colorID: 0
position: 183,767
---
Dan: Well there’s always a chance someone will die #line:e4db66
Dan: And I can have their job. #line:2fb23b
Mae: Dan, you’re not gonna like #line:631b43
Mae: kill anyone, are you? #line:a51abb
Dan: Does it pay? #line:be25c9
===
title: Dan_00_Someone
tags: demo2
colorID: 0
position: 537,763
---
Dan: I’ve been out of work for a year before. #line:0a16d7
Dan: And this time I only lasted 6 months before being laid off. #line:89e77e
Dan: Do you know what that does to a resume? #line:c03c3e
Mae: Not really. #line:00278b
Dan: I have a zombie resume. #line:d4bfe1
Dan: It’s dead but somehow it’s still going all over the place. #line:0e4484
Mae: Oh. #line:7f148b
===
title: DanA2D1_00
tags: 
colorID: 3
position: -1677,266
---
// olpickaxe
Mae: how's it going dude? #line:d9a678
Dan: got a job here. that's cool. #line:e463e3
Mae: oh nice! #line:421f0f
Dan: actually i'm lying. it sucks. #line:690a0e
Mae: oh. geez. #line:4c90fb
===
title: DanA2D1_01
tags: 
colorID: 3
position: -1688,500
---
Dan: I had no idea there were so many kinds of hammers #line:50fb32
Dan: and wrenches #line:ad7fe4
Dan: and nails. #line:c6b3ba
Dan: my god, the nails. #line:ccfac7
===
title: DanA2D3_00
tags: 
colorID: 3
position: -1401,266
---
// olpickaxe
Mae: hey dude. #line:af3eb6
Dan: hello. #line:42c0fe
Mae: how's working here going? #line:eadcac
Dan: eh. i dunno. #line:1308b7
Dan: it pays. #line:c7a891
===
title: DanA2D3_01
tags: 
colorID: 3
position: -1403,503
---
Dan: I like refuse to unload all that rock salt by myself. #line:1a70e6
Dan: it's not even halloween yet. #line:5a914b
===
title: DanA3D1_00
tags: 
colorID: 4
position: -1150,271
---
// tcw, behind diner
Mae: hey dude! #line:743654
Dan: hey... you? #line:f75680
Mae: working here now? #line:a85320
Dan: yep. sucks. #line:a1f16f
Dan: but it pays. #line:668985
Mae: question: how do they fit a kitchen in there? #line:223d9b
Dan: barely. #line:d9faf2

===
title: DanA3D1_01
tags: 
colorID: 4
position: -1152,503
---
Mae: What do you do here? #line:5bd36b
Dan: Grillman and janitor. #line:51fd7c
Dan: i cook and mop. #line:d72061
Mae: at the same time? #line:ba5da2
Dan: yes. #line:3ac598
===
title: DanA4D3_00
tags: 
colorID: 6
position: -885,273
---
// taco
Mae: so you're a taco dude now? #line:97e9dc
Dan: i mean i was hired to be a taco artisan. #line:147614
Dan: but i guess that means holding this sign out in the snow. #line:47f2cb
Mae: it's not really snowing that hard. #line:3d98f5
Dan: it's snowing. there is snow in the sky, falling. #line:9a6141
Mae: how are the tacos here? #line:f6bc88
Dan: good. i like tacos. #line:d54f68
Mae: there is not one person on god's earth who doesn't love tacos. #line:24f0a6
Dan: bad news: my ex hates tacos. #line:ae73b6
Mae: good news: they're now your ex. #line:15153e
Dan: heh heh heh #line:8b8408
Mae: have fun with the tacos. #line:90e683
Dan: eh. it pays. #line:768c3f
<<set $talked_to_dan_about_taco_buck to 1>>
===
title: DanA4D3_01
tags: 
colorID: 6
position: -881,505
---
Dan: TACO Buck! #line:be250a
Dan: Dine in, Dine out! #line:0558d8
Dan: We deliver! #line:ae1ee5
Dan: Please tip your delivery persons! #line:c8e3ae
Dan: best tacos in all of this general region. pretty sure. #line:349429
Dan: been missing the pizza place? #line:c49ec2
Dan: why not try a pizza taco? #line:56f913
Mae: oh wow. that sounds amazing. #line:14f6c1
Dan: watching your health? #line:826bb1
Dan: try out all-lettuce taco! #line:86d783
Mae: no. i refuse. #line:54cc23
Dan: get outta here kid i'm workin'. #line:d488e5
===
title: DanIntro
tags: 
colorID: 0
position: -864,-204
---
Mae: Danny, right? #line:a1b7cb
Dan: Yep. Well, Dan. #line:04352a
Mae: I think you were a senior when I was a freshman? #line:b768dc
Dan: Probably. #line:0a68f1
<<set $met_dan to 1>>

<<if $act is 2 and $day is 1>>
    [[DanA2D1_00]]
<<elseif $act is 2 and $day is 3>>
    [[DanA2D3_00]]
<<elseif $act is 3 and $day is 1>>
    [[DanA3D1_00]]
<<elseif $act is 4 and $day is 3>>
    [[DanA4D3_00]]
<<endif>>
===
