title: InitLevel
tags: 
colorID: 0
position: -83,97
---
<<set $taxi_stop to 2>>

<<if $act is 2 and $day is 2 and $church_hill_background_a2d2_done is 0>>
    <<show Molly>>
    <<setCanInteract Molly 0>>
    <<warp Molly MollyCamp>>
<<else>>
    <<hide Molly>>
<<endif>>

<<if $act is 2 and $day is 4>>
    // show wyrd teens
<<elseif $act is 4 and $day is 3 and $found_pentagrams >= 3>>
    // show wyrd teens
<<else>>
    <<hide Britt>>
    <<hide Pam>>
    <<hide Karla>>
<<endif>>

<<if $act is 3 and $day is 2 and $big_fight is 0>>
    <<set $church_locked to 1>>
<<else>>
    <<set $church_locked to 0>>
<<endif>>

<<if $jen_done>>
    <<hide Jen>>
<<elseif $act is 3 and $day is 2>>
    <<show Jen>>
//<<elseif $act is 2 and $day is 4 and $did_gregg_friendship_quest_3>>
//    <<show Jen>>
<<else>>
    <<hide Jen>>
<<endif>>
===
title: EnterLevel_A2D1
tags: 
colorID: 0
position: 189,104
---
<<set $reminder_east_stairs_done to 1>>
===
title: ChurchClosedDemo2
tags: 
colorID: 3
position: 618,161
---
Mae: Huh. #line:ae67c7
Mae: Door's locked. #line:079778
Mae: What if I needed, #line:62e620
Mae: like, #line:61107c
Mae: Sanctuary from vampires or something. #line:b68e1c
Mae: This is just irresponsible. #line:7de9da
<<end>>
===
title: Cliff
tags: 
colorID: 3
position: 383,480
---
Mae: Huh. Never been up here. #line:ac3c43
Mae: Looks like someone else has been, and recently. #line:8e2f2b
<<lookDown Mae>>
Mae: something on the ground, drawn with ash... #line:4b12c3
Mae: -_- #line:9c027d
Mae: {emote=pentagram} #line:677dee
<<close>>
Mae: o> #line:a28ac3
Mae: huh. #line:e96c0c
<<lookNearest Mae>>
Mae: o_o #line:0f1e26
Mae: something's been going on out here. #line:0d2d69
Mae: Could be teens praying to whatever god teens pray to now #line:2d5385
Mae: Could be... #line:a44d9e
Mae: -_- #line:6494ca
Mae: hmmm. #line:23e79f
Mae: _o_ #line:9a0005
Mae: o_o #line:28a499

<<end>>
===
title: Statue_Interaction
tags: 
colorID: 3
position: 650,488
---
Mae: Saint Rubio. #line:f4f021
Mae: I think this guy breathed fire and ate people or something. #line:a0eb3f
Mae: And now he's a statue. #line:7bdba6
Mae: In Possum Springs. #line:384a9e
Mae: Because life is weird sometimes. #line:55a0f2
<<end>>
===
title: Quiet
tags: 
colorID: 3
position: 923,481
---
Mae: Quiet up here. #line:7a75bd
Mae: Most people up here are dead I guess. #line:24ffae
<<end>>
===
title: WyrdTeens_A2D4
tags: 
colorID: 3
position: 1274,196
---
<<set $met_wyrd_teens to 1>>
Mae: Hey. #line:4b0491
Britt: ... #line:a4a7f4
Mae: Hello. #line:545a16
Pam: ... #line:c2a723
Mae: HEY!!! #line:e03e92
Karla: traveler, from distant lands #line:d4b521
Britt: you have journeyed far #line:6776b0
Pam: and it has brought you here #line:337da7
Karla: to us #line:82b6f0
Mae: Actually I walked like 15 minutes. From my house. #line:8495d8
Britt: walker, far from home #line:a765e7
Pam: we will tell of your future #line:eb97e8
Karla: but first #line:f8d424
Britt: be truthful: #line:5bcc37
Pam: tell us #line:97cc07
Karla: which of these have you seen #line:59dc1a
Britt: a shipwreck? #line:e16845
Pam: a ghost on a hill? #line:162344
Karla: an arrow, brok'n? #line:1aba64
Mae: “brok’n?” really? #line:49c1bb
Mae: I... um... hm #line:d2382d
Mae: you’re all like 14 year old goths. Why am I answering this? #line:f1b9c7
<<close>>
<<wait 2>>
<<shuffleNextOptions>>
->the shipwreck #line:583cd8
	Mae: on my way into town #line:f6cd67
	Mae: in the playground... this old ship #line:45aa48
	Mae: how did you...? #line:b977a6
	[[Shipwreck]]
->the ghost #line:3df4da
->the broken arrow #line:f688c2
Britt: False #line:66ac39
Pam: You have failed #line:e70a79
Karla: We cannot help you #line:28c5de
Britt: You may see us once more #line:60ed2a
Pam: But it is not certain #line:03741c
Karla: And then never again. #line:873c94
Mae: Ok. #line:432a9a
Mae: Uh. #line:a66ace
Mae: Have fun, kids. #line:47a867
Britt: Fun is #line:a4d6cb
Pam: all we #line:c24c30
Karla: have. #line:14b6eb
Mae: do you like, practice that talking in turn thing? #line:a7a775
Karla: ... #line:d9afc1
Britt: you’re not gonna narc on us to the pastor, right? #line:aa9c94
Pam: for being out here? #line:1fb7c6
Mae: nah. #line:bd2ee3
Karla: cool thanks #line:384fb7
<<set $wyrd_teens_done to 1>>
<<unlockDoodle WeirdTeens>>
===
title: Shipwreck
tags: 
colorID: 3
position: 1564,221
---
Britt: You have answered well #line:a12be0
Pam: We will peer into your future #line:ccd862
Karla: And give you what help we can #line:5d2419
Britt: In the coming dark #line:470f25
->what, Daylight savings ending? #line:9a7361
    Pam: ha #line:3ae68e
    Karla: ha #line:30b401
    Britt: ha. #line:52c781
    Mae: do you like, practice that talking in turn thing? #line:594aca
->alright go for it #line:22275a
Pam: tell us first #line:4c6b1c
Karla: what is god #line:16653b
Britt: in this place #line:cc1e61
Mae: wow. Uh. #line:e9712b
Pam: is it #line:8f0ad8
Karla: caring but absent? #line:adff0a
Britt: uncaring but distant? #line:ccd16d
Pam: vicious and roaming? #line:9bac76
Mae: can we go with “nice and helpful?” #line:5bcffa
<<close>>
<<wait 2>>
Mae: ok fine. #line:4678bc
->caring and absent #line:e823cb
	Karla: true enough #line:f16139
	Britt: she works still #line:1233b3
	Pam: in hopes it will return #line:3c917a
	Mae: she? She who? #line:53d713
	Karla: we see something #line:060379
	Britt: In your future #line:a21a45
	Pam: we know! #line:f8886b
	Karla: we know! #line:471d32
	Britt: we know! #line:388512
	Mae: ok already! Geez! #line:80c5f7
    Pam: if the sky is empty #line:be390a
    Karla: is it enough #line:55a186
    Britt: to be a good watcher? #line:78d1e6
    Mae: that’s more a question than a fortune, guys #line:5c538c
->uncaring and distant #line:71978f
	Karla: i can see it #line:1f9d14
	Britt: on a distant shore #line:4c9718
	Pam: beneath the stars #line:069e83
	Mae: ok #line:26069c
	Karla: we see something #line:d2c01b
	Britt: In your future #line:347513
	Pam: we know! #line:e9dfcf
	Karla: we know! #line:59ffb2
	Britt: we know! #line:31432b
	Mae: ok already! Geez! #line:d8b86a
	Pam: you will swim out to sea #line:d8db05
	Karla: and meet it #line:85ac12
	Britt: on a distant shore #line:f9a86b
	Mae: we’re like 6 hours from the ocean. #line:0cde53
	Mae: you guys are bad at this. #line:0ef01d
->vicious and roaming #line:18a19d
	Karla: it ate our futures #line:c3e7cc
	Britt: and moved on #line:76b626
	Pam: but we live in the ruins #line:502bec
	Mae: uh huh #line:2f9451
	Karla: we see something #line:b436c4
	Britt: In your future #line:6bc2e0
	Pam: we know! #line:34db72
	Karla: we know! #line:5de1e6
	Britt: we know! #line:ce5046
	Mae: ok already! Geez! #line:34e315
	Pam: they cry out to it #line:8e99bb
	Karla: but it is others #line:5a3f15
	Britt: who will sacrifice #line:122d97
	Mae: ok cool well #line:13e60a
	Mae: that’s cryptic #line:959f44
Mae: let’s try something easier.		 #line:0de556
Mae: What am I going to have for dinner tonight? #line:93cfa4
Pam: don’t know, don’t care #line:1638f1
Karla: i want tacos #line:51ab7d
Britt: we’ll have them soon #line:ad3459
Pam: we’ll all have tacos soon #line:4f155d
Mae: ok well i’m gonna get going #line:553176
Karla: STOP. #line:207220
Britt: do you see the mystical symbol #line:1563af
Pam: etched upon this sacred rock? #line:3d227c
->the pentagram you guys drew? #line:2ec1cd
	Karla: yes. good. #line:e0f087
->the spooooky pentagram you teens drew?  #line:976187
	Karla: shut up. it’s full of mystical shit. #line:63d90f
Mae: ok? #line:7afc0d
Britt: it is the first of three #line:805766
Pam: find it thrice #line:a871da
Karla: and it shall find you #line:ebaa17
Britt: in the dark #line:382d14
Mae: ok cool well #line:749d45
Mae: iiiiii’m gonna head out #line:0a0fd0
Mae: you guys are adorable #line:6e0d1e
Mae: and weird. But the fun kind of weird. #line:260d35
Pam: this is serious #line:c49b21
Karla: like we’re not kidding #line:f090ac
Britt: we don’t care or anything #line:eac1a2
Pam: but like #line:eeb532
Karla: dude. #line:384e3c
Mae: alright alright i’ll keep an eye out #line:f12387
Mae: for pentagrams and what not #line:44265f
Karla: cool #line:da35fd
Mae: ha ha you broke your speaking order #line:d7b237
Pam: um no #line:4619a8
Britt: we’re just going backwards now #line:9ba3e6
Karla: are you freaking out yet #line:cad314
Mae: see ya, weird goth teens #line:450b61
Pam: you’re not gonna narc on us to the pastor, right? #line:6f7320
Britt: for being out here? #line:a7fced
Mae: nah. #line:5fa9fa
Karla: cool thanks #line:bfdac0
<<set $pentagram_quest to 1>>
<<set $wyrd_teens_done to 1>>
<<unlockDoodle WeirdTeens>>
===
title: WyrdTeens_A4D3
tags: 
colorID: 6
position: 1847,250
---
Mae: Hey. #line:6eb655
Mae: It’s you guys. #line:564406
Britt: sup #line:3ac83c
Pam: hi #line:f271d5
Karla: heeyyyy #line:8574f7
Mae: you guys still doing that talking in order thing? #line:d46159
Pam: nah #line:53eaea
Britt: kept messing it up #line:fa08d7
Pam: we’re kinda over the whole thing anyway #line:535438
Mae: oh really? #line:b48bae
Karla: yep. #line:e627e0
->because that pentagram thing was totally a thing #line:6e94fd
	Mae: so like #line:9f48c4
	Mae: thanks #line:8b8f74
	Pam: huh. Cool. #line:9fd837
	Mae: and actually everything you said was pretty spot on #line:6dc26f
	Britt: probably because we’re actually psychic #line:519dd0
	Karla: probably #line:611ad1
	Mae: alright well I can’t tell what level of sarcasm we’re working on here #line:c272b7
	Mae: oh god i sound like a mom #line:60602b
	Pam: maybe no sarcasm #line:1ce339
	Karla: maybe we’re covering our tracks #line:2f4e98
	Britt: so our cover isn’t blown #line:d13d4f
	Mae: never change, weird teens #line:62ec7e
	Mae: please grow and become weird adults #line:570f95
	Pam: maybe we don’t age #line:5147de
	Karla: maybe we stay young #line:454ca1
	Britt:{width=8} maybe we’re F O R E V E R. #line:9905fb
	Mae: thank you, again. #line:35f0ac
	Pam: for what? #line:10e445
	Karla: i don’t remember doing anything #line:0074ed
	Britt: who is this lady? #line:8c001d
->we all go through phases #line:da0082
	Mae: I totally went through a witch phase. #line:785254
	Mae: Had one of those mirrors. #line:05e6f0
	Mae: Then I dropped it and broke it. #line:b3cbc9
	Karla: bad omen #line:3344e2
	Britt: for reals #line:339b5f
	Mae: would’ve been good to know that back then #line:3f262a
	Mae: hey all that stuff you said before #line:1c2f53
	Mae: actually kinda ended up being spot on #line:96d6b7
	Britt: nice #line:f4ac5b
	Karla: niiiiiiiiiiice. #line:e4fa03
	Mae: I had a really weird and bad week #line:3c97d9
	Mae: but #line:5dfe7f
	Mae: the snow is nice. #line:6ba0ff
	Pam: seems to have worked out #line:fdb1b4
	Pam: the mirror probably did it  #line:a6d0e4
	Britt: what dude #line:101472
	Pam: dunno. I’m literally just making stuff up now #line:2afadc
	Mae: cool. Well, #line:d748f2
	Mae: i’ll see you around #line:1dd550
	Karla: yep #line:46e21c
	Britt: probably will #line:9d3743
	Pam: it’s a small town. #line:5f7a94
<<set $wyrd_teens_done to 1>>
===
title: FrontOfStatueA2D4
tags: 
colorID: 3
position: -25,881
---
Mae: breath some fire already dude #line:51d31c
<<end>>
===
title: FrontOfStatueA3D4
tags: 
colorID: 4
position: 222,880
---
Mae: we got statues of stern dudes for miles here in possum springs. #line:26f56e
<<end>>
===
title: FrontOfStatueA4D3
tags: 
colorID: 6
position: 461,876
---
Mae: he'll look a lot friendlier when he's covered in snow. #line:698aec
<<end>>
===
title: FrontOfWatertower
tags: 
colorID: 0
position: 9,1159
---
Mae: I always wanted to climb the water tower. #line:8f8557
Mae: but then that one kid died doing that. #line:2e6561
Mae: whatever. #line:83197a
Mae: i'll get to it. #line:0e9e49
Mae: probably see clear to briddle from there. #line:6102db
<<end>>
===
title: ChurchLocked
tags: 
colorID: 3
position: -18,441
---
Mae: Who locks a church? Mom must be out today... #line:1d638c
Mae: What if I needed, #line:a3a768
Mae: like, #line:e5b2f3
Mae: Sanctuary from vampires or something. #line:e294e0
Mae: This is just irresponsible. #line:61ae2d
<<end>>
===
title: Jen
tags: 
colorID: 4
position: 918,-7
---
Mae: Hey Jen! #line:9961d4
Jen: Hey... Mae Borowski? #line:d7c3aa
Mae: yeah.  #line:6c9355
Mae: what are you doing back here? #line:3a270f
Mae: Does gregg know you’re back? #line:1277a5
Jen: Nah. Just here for a day. #line:44eb7d
Jen: Had to pick some stuff up from Gregg’s mom. #line:2b9bff
Jen: Hauling back to school tonight. #line:6bb3d0
Mae: ah. #line:37e936
Jen: didn’t you go to school? What are *you* doing here? #line:f2b6bf
-> dropped out. Kinda. #line:733ed0
    <<set $jen_truth to 1>>
    // (track this variable - truth)
	Jen: oh. Sorry to hear that. #line:595de1
	Mae: eh. Whatever. #line:aa76fa
	Mae: what are you going for? #line:35382e
	Jen: i wanna do fashion design. #line:6e0b1e
	Mae: is that a job you can get? #line:5ea7ba
	Jen: theoretically. #line:f30ef5
-> same as you. Just back for a day. #line:f74b7e
    <<set $jen_lie to 1>>
    //(track this variable - lie)
	Jen: what school did you end up going to? #line:b992d1
	Mae: Durkillesburg. #line:eec8d9
	Jen: Ah. #line:18d7c0
	Mae: You’re out in Bright Harbor, right? #line:b413d3
	Jen: yeah. #line:8cd65b
	Mae: how’s that? #line:c9b9f6
	Jen: good. Expensive. But good. #line:a41608
<<wait 2>>
Mae: you still go crossbow hunting? #line:10c0c9
<<if $did_gregg_friendship_quest_3>>
	Mae: Gregg still does. Kinda. #line:9915d9
<<else>>
	Mae: Gregg used to have a crossbow too I think? #line:88386e
<<endif>>
Jen: yeah I still hunt rabbits and stuff. #line:93a97f
Jen: nothing big. #line:c79677
Jen: don’t get much chance at school. #line:98f070
Jen: i was shootin’ at that tree over there #line:784b8b
Jen: then i notice there was a dude sleeping behind it #line:890c1e
Jen: so i stopped. #line:9c0386
<<if $met_bruce>>
	Mae: oh yeah that’s Bruce. #line:0d6f10
	Mae: he’s nice. #line:3850c8
<<else>>
	Mae: oh geez yeah. #line:be6787
	Mae: wonder who that is? #line:9d21e2
<<endif>>
Jen: well I’m just glad I didn’t accidentally put a bolt in his face. #line:0b5daa
Mae: oh yeah. That’d be bad. #line:01f69e
<<wait 2>>
Mae: well, it was nice seeing you? #line:719b44
Jen: you still see gregg sometimes? #line:f3c6c3
<<if $jen_truth>>
	Mae: oh yeah. All the time. #line:19e9e6
<<else>>
	Mae: oh. You know. Just when I’m home. sometimes. #line:343eae
<<endif>>
Jen: well tell that cousin of mine he needs to come see me out in Bright Harbor. #line:f64c1b
<<if $knows_bright_harbor>>
	Mae: oh. Uh. yeah. Will do. #line:9839d8
	<<wait 2>>
	Mae: he’s not leaving here. #line:d50780
	Jen: oh i dunno. I think he should. #line:d8a27f
	Jen: guy’s gotta spread his wings. #line:d9661a
	Mae: maybe his wings are just fine here. #line:f2f7bd
	Jen: nah. #line:bebbd1
    Jen: Gregg’s too much for this town. #line:b09873
	Mae: ha ha. No argument there. #line:2c2022
<<else>>
	Mae: sure thing! #line:f3cc07
<<endif>>
Mae: seeya #line:48d66d
Jen: take it easy! #line:e54e00
<<set $jen_done to 1>>
<<unlockDoodle Jen>>
===
