title: InitLevel
tags: 
colorID: 0
position: -1847,539
---
<<if $act is 2 and $day is 4 and $did_harfest_play is 0>>
    <<show Crusty1>>
    <<show Crusty2>>
    <<show OldHobo>>
<<endif>>

<<if $act is 3 and $day is 1>>
    <<if $trigger_molly_a3d1_done is 0>>
        <<show Molly>>
    <<endif>>
<<else>>
    <<hide CopCar>>
<<endif>>

<<if $act is 3 and $day is 3 and $crusty_3_done is 0>>
    <<show Crusty3>>
<<endif>>

<<if $act is 2 and $day is 1 and $gregg_friendship_quest is 1>>
    <<show Gregg>>
    <<warp Mae GreggFQ1Mae0>>
    <<warp Gregg GreggFQ1Gregg0>>
    <<cameraLookAt GreggFQ1CameraStart>>
    <<playerFace -1>>
<<endif>>

<<if $act is 3 and $day is 4>>
    <<if $walking_up_to_the_woods>>
        <<show Angus>>
        <<show Bea>>
        <<show Gregg>>

        <<warp Angus WalkUpStartAngus>>
        <<warp Bea WalkUpStartBea>>
        <<warp Mae WalkUpStartMae>>
        <<warp Gregg WalkUpStartGregg>>
    <<endif>>
<<endif>>
===
title: EnterLevel_A2D1
tags: 
colorID: 3
position: -1256,735
---
<<if $gregg_friendship_quest is 1>>
    [[GreggFQ1]]
<<endif>>
===
title: GreggFQ1
tags: 
colorID: 3
position: -1015,680
---
<<fadeIn 2 wait>>
<<wait 1>>

<<walk Mae GreggFQ1Mae1>>
<<walk Gregg GreggFQ1Gregg1 1>>
<<wait 1>>
Gregg: And they found him out in the back of the junkyard! #line:84d577
<<close>>
<<wait .5>>

<<waitForMove Gregg>>

Mae: That's upsetting.  #line:3d3707
Gregg: He'd been there for a while. #line:9cf991
Mae: This is depressing. :\ =_= #line:924b69
Gregg: Really?  #line:ab868e
Gregg: I just thought it was like super raaaawwww.:) #line:8c1de7
Mae: No, I mean the abandoned Food Donkey. o_o #line:03e63b
Gregg: Yeah I know, right? It was a shock. :| #line:b1febc
<<rotateHead Mae -10 .5>>
Mae: I think in 2nd grade we colored pictures for thanksgiving. :| o> #line:65a81b
Mae: and they were like Food Donkey themed. #line:c9b407
<<lookUp Mae>>
Mae: and they put them up on these windows. #line:08c647
Gregg: I remember that! #line:019da0
<<lookNearest Mae>>
Gregg: It was a donkey dressed up as a turkey! #line:84ab4c
<<lookOut Mae>>
<<dilate Mae .85 1>>
Mae: Yeah! Horrifying! :< #line:235af5
Gregg: Really, really horrible. #line:fb1945
Mae: :| _o_ #line:0e8960
<<close>>
<<cameraLookAt GreggFollow 1>>
<<rotateHead Mae 0 .5>>
<<wait 2>>
<<lookNearest Mae>>
<<dilate Mae 1 .5>>

<<walk Gregg GreggFQ1Gregg2 1>>
<<wait .5>>
<<walk Mae GreggFQ1Mae2 1>>
<<extraZoom -5 7>>
<<cameraLookAt GreggFQ1Focal 7>>
<<lookLeft Gregg>>
Mae: What did they do with all the food? #line:11eb5c
Gregg: Donkey-turkeys got 'em. #line:cd961d
Mae: Donkturks. #line:d45b3f
<<lookNearest Gregg>>
Gregg: Durnkeys. #line:863de7
<<close>>

<<waitForMove Mae>>
<<wait .5>>
<<lookRight Gregg>>
Mae: So what are we doing here? #line:946c35
Gregg: CRIMES. =_= #line:8d279b
Mae: Ok I'm like double-in now. :)  #line:852d51
Gregg: I've been livin' too clean, Mae. #line:b1bf01
Mae: Damn right. =_= <o> #line:c58032
Gregg: We run this town. #line:caabfd
Mae: Why don't we go in? o_o :| #line:fca771
Gregg: O_O D: \o/ #line:7cf6d5
Gregg: HEY!!!!! #line:8f14c0
<<close>>
<<wait 1>>
Mae: What the hell are you doing? _o_ #line:b8172e
Gregg: o_o :| #line:a96adc
Gregg: I think I... yeah cool I got his attention. #line:a1e74e
Mae: What? #line:a80b2f
<<close>>

<<wait 1>>
Gregg: _o_ #line:12f440
Gregg: Aw yeah here he comes. #line:1c1d60
Mae: Who? #line:d2502b
Gregg: You don't know him I don't think. #line:b6f5c7
Mae: Try me. #line:c9963f
<<close>>

<<trackAllSpeechBubblesToCharacters 0>>
<<speechBubbleDistanceOverride 0>>

<<irisOut 1 wait>>
<<scene FoodDonkeyInterior>>
===
title: TriggerMolly
tags: 
colorID: 0
position: -1483,452
---
<<lookAt Molly Mae>>
Molly: What were you doing out here last night? #line:132996
    -> I saw someone kidnap someone. Like, a kid. #line:6cec57
        <<wait .5>>
    -> What were YOU doing out here last night? #line:aa3974
        Molly: Vagrants. Reports of. #line:0009a7
        Molly: That's why #line:561fb3
        //(waves hand at council)
        Molly: all this. #line:930906
        Molly: might've just been someone saw you out here. #line:8d9c59
        Molly: Your turn #line:6dfa97
        Mae: I saw a kidnapping. #line:1605b8
Molly: Tell me what you saw again? #line:71450b
Mae: Someone in a big old coat with someone over their back. #line:82a5c9
Molly: Uh huh. #line:4c4122
Molly: And you chased them down here? #line:979c6f
Mae: Yeah and I got to the fence and they were up in the woods. #line:291d3a
Molly: Alright. Thank you, Mae. #line:de7646
Mae: Ugh. I knew you were just gonna brush this off. #line:232c68
Molly: Mae, there's been no missing person reports. #line:4d02ec
Molly: That *tends* to happen when a kid goes missing from something like Harfest. #line:f7addb
Molly: Hell, I got 6 calls about kids out too late. #line:b809b6
Molly: There would be a report. #line:eb3a90
Mae: I'M REPORTING!! This! This right here! This is a report! #line:cccb36
Molly: Mae, there's no break in the fence. #line:e0da91
Mae: So? #line:8f7676
Molly: So unless someone could jump this thing... #line:86ef66
Molly: while carrying a teenager... #line:22e9fc
Molly: who hasn't been reported as missing... #line:ad90ec
Mae: ... #line:20492a
Molly: Any theories, Mae? #line:66def8
Mae: ... #line:599271
    -> No. #line:71b70c
    -> They could've gone around the fence! #line:073d95
        Molly: I dunno Mae, #line:28c91a
        Molly: this fence goes out along the parking lots #line:579398
        Molly: almost to the tracks. #line:48551c
        Molly: For you to be right behind them, #line:ba6c86
        Molly: and then to be miraculously on the other side of the fence? #line:676ee7
        Mae: UUUUUUUUURGH. #line:dc0473
Molly: I was running around half the night #line:94628d
Molly: getting after teenage bullshit #line:3707e8
Molly: and people scared of clotheslines and noises. #line:f2244b
Mae: I- you- y-you- aaaargh!!! #line:2422a7
Mae: Are you *TRYING* to kick off a horror movie? #line:57859a
Mae: No one believes the girl who saw the ghost! #line:b4a644
Mae: Well, I have bad news for you- #line:8b6265
Mae: THE COP ALWAYS DIES. #line:fa510b
Molly: I'm sorry, we're talking about a “GHOST” now? #line:42d39d
Mae: It must've gone through the fence?? #line:207abe
Molly: Really, Mae? #line:804da1
Mae: No! Yes! I don't know! #line:c37108
Mae: I'm really tired. #line:84dec7
Molly: Yeah that's kinda what I thought. #line:c5f153
<<close>>
<<lookNearest Molly>>
<<walk Molly MollyToRight wait>>
Mae: Will you at least check the woods? #line:246f85
<<lookAt Molly Mae>>
Molly: Mae, me and Dan McConnell were up there this morning #line:1a4674
Molly: because of what YOU said last night. #line:cbbb37
Molly: From the fence up to the old mine. #line:fccb09
Molly: We found a couple teenagers on a gross mattress, #line:324f05
Molly: we saw some worried-looking deer, #line:dd75e1
Molly: and one deer hunter out too early. #line:234a0e
Mae: That can't be it. #line:dd1faa
Molly: That is it. #line:a88ec6
<<close>>
<<walk Molly MollyOffRight wait>>
<<hide Molly>>
<<hide CopCar>>
Mae: THE COP ALWAYS DIES! #line:72bfdb
===
title: PreInitLevel
tags: 
colorID: 0
position: -1851,225
---
<<hide Gregg>>
<<hide Molly>>
<<hide Crusty1>>
<<hide Crusty2>>
<<hide Crusty3>>
<<hide OldHobo>>
===
title: EnterLevel_A2D4
tags: 
colorID: 3
position: -673,389
---
<<if $did_harfest_play>>
    <<hide Germ>>
    <<warp Mae MaeRight>>
    [[Harfest]]
<<else>>
<<endif>>
===
title: Harfest
tags: 
colorID: 3
position: -469,705
---
<<playMusic after_kidnapping>>
<<run Mae MaePastFoodDonkey wait>>
Mae: *pant pant* #line:26bd61
Mae: where the hell did he go? #line:146826
<<playOneShot event:/harfest/chain_fence_clang ChainFenceSFX>>
Mae: Shiiiit. #line:a44eac
<<close>>
<<set $harfest_block_right to 1>>
===
title: TriggerLeftSide
tags: 
colorID: 3
position: -207,586
---
//camera pulls back/pans up so you can see the miner with the kid draped over his
//shoulder. there’s a solid few seconds of frozen poses here as the wind blows
//leaves around etc//
<<playMusic eide>>
<<cameraLookAt CameraLookAtEide 3>>
<<wait 10>>
//a flashing pops on behind Mae and the camera pulls back down to normal view,
//losing the guy on the hill//
<<cameraLookAt CameraBackToMaeAfterEideHill 3>>
//<<extraZoom 2 3>>
<<wait .5>>
<<close>>
<<warp Molly MollyOffRight>>
<<show Molly>>
<<walk Molly MollyToRight wait>>
<<lookAt Molly Mae>>
Molly: Mae. #line:6a0776
Molly: Go home. #line:59dcaf
<<lookNearest Molly>>
<<hide EideHarfestStand>>
Mae: There's a guy up there on the hill! #line:f4ad8a
Mae: he stole some kid? #line:4f4752
Mae: he looked like a ghost! #line:6eb0b3
//molly shines a light up//
Molly: a ghost, huh? #line:77075e
Mae: Or something like that? #line:4727f4
//molly shines light back at mae//
Molly: Mae, you shouldn't be out here #line:b8a05e
Molly: doing whatever it is you're doing. #line:c0a5f2
Mae: NO! NO ONE SHOULD BE OUT HERE! #line:c5e56a
Mae: But there's this guy- #line:c717e8
Molly: I'll drive you home, Mae. #line:75459b
Mae: NO! #line:2c344c
Molly: MAE! NOW. #line:041410
<<close>>
<<irisOut 1 wait>>
<<set $harfest_costume to 0>>
<<scene BlackoutWoods>>
===
title: EnterLevel_A4D2
tags: 
colorID: 5
position: 443,111
---
<<if $night and $boss_convo>>
    [[FromWellWoods]]
<<elseif $night>>
    [[LimpIn]]
<<endif>>
===
title: FromWellWoods
tags: cut
colorID: 5
position: 690,408
---
Mae: We're saying goodbye to each other after the well woods. #line:8c9e5a
Angus: Yes. #line:e0165e
Gregg: Yes. #line:8d162d
Bea: Yes. #line:9ffb88
<<close>>
<<irisOut 1 wait>>
<<scene MaeHouse left>>
===
title: LimpIn
tags: 
colorID: 5
position: 337,398
---
<<set $death_walk to 1>>
<<playIdleAnimationPlayer>>
<<cameraLookAt CameraInFrontOfFoodDonkey 0>>
<<warp Mae MaeLimpInFrom>>
<<walk Mae MaeLimpInTo wait>>
<<cameraFollowPlayer 2>>
===
title: MidwayThroughBasketBallCourt
tags: 
colorID: 5
position: 306,636
---
<<fadeOut 1 wait>>
<<scene MineWoods left>>
===
title: EnterLevel_A3D4
tags: temp
colorID: 4
position: -12,165
---
<<if $walking_up_to_the_woods>>
    <<walk Angus WalkUpEndAngus>>
    <<walk Bea WalkUpEndBea>>
    <<walk Mae WalkUpEndMae>>
    <<walk Gregg WalkUpEndGregg>>
    Mae: We're talking about walking to the woods. #line:a36df0
    Bea: Talking and walking. #line:d4b3a9
    Angus: Yes we are. #line:abc5b8
    Gregg: Indeed. #line:e2ce10
    <<set $walking_up_to_the_woods to 0>>
    <<fadeOut 1 wait>>
    <<scene MineWoods>>
<<endif>>
===
title: FarLeftFence.1
tags: 
colorID: 0
position: -939,-258
---
Mae: how did he get over this thing? #line:0e82a5
Mae: i can’t jump that. No way he could have. #line:8bd861
Mae: there’s no hole. #line:4b8a13
Mae: how did he do it? #line:881d81
===
title: FarLeftFence.2
tags: 
colorID: 0
position: -697,-261
---
Mae: i mean i guess he could really be a ghost #line:89f906
Mae: i think he’s a ghost #line:b8ba65
Mae: ugh. #line:b742e3
Mae: ...what’s out there? #line:8e7922
<<end>>
===
